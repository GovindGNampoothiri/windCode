;+
;*****************************************************************************************
;
;  FUNCTION :   find_1d_cuts_2d_dist.pro
;  PURPOSE  :   This routine calculates the cuts of a regularly gridded distribution
;                 function, f(x,y), along two orthogonal vectors with intersection at
;                 {X_0, Y_0}.  The vector directions are defined by the optional
;                 keyword, ANGLE.  If this is not set, then the x-coordinate output
;                 corresponds to a line along the horizontal axis offset by X_0 and the
;                 y-coordinate output corresponds to a line along the vertical axis
;                 offset by Y_0.
;
;  CALLED BY:   
;               NA
;
;  CALLS:
;               find_frac_indices.pro
;
;  REQUIRES:    
;               1)  UMN Modified Wind/3DP IDL Libraries
;
;  INPUT:
;               FXY      :  [N,N]-Element array defining the regularly gridded
;                             distribution function, f(x,y)
;               X__IN    :  [N]-Element array of regularly gridded coordinates
;                               corresponding to the 1st dimension in some function,
;                               f(x,y) [e.g., x-coordinate or independent coordinate]
;               Y__IN    :  [N]-Element array of regularly gridded coordinates
;                               corresponding to the 2nd dimension in some function,
;                               f(x,y) [e.g., y-coordinate or dependent coordinate]
;
;  EXAMPLES:    
;               test = find_1d_cuts_2d_dist(fxy,x__in,y__in)
;
;  KEYWORDS:    
;               X_0      :  Scalar defining the X-offset from zero for the center of
;                             the X-vector cut [e.g., parallel drift velocity]
;                             [Default = 0.0]
;               Y_0      :  Scalar defining the Y-offset from zero for the center of
;                             the Y-vector cut [e.g., perpendicular drift velocity]
;                             [Default = 0.0]
;               ANGLE    :  Scalar defining the angle [deg] from the Y-Axis by which
;                             to rotate the [X,Y]-cuts
;                             [Default = 0.0]
;               FLOG     :  If set, routine will use the natural log of FXY before
;                             interpolating to the locations defined by the cuts
;                             [Default = FALSE]
;               NX       :  Scalar [long] defining the # of elements along the
;                             x-coordinate to use for the output cut
;                             [Default = N_ELEMENTS(X__IN)]
;               NY       :  Scalar [long] defining the # of elements along the
;                             y-coordinate to use for the output cut
;                             [Default = N_ELEMENTS(Y__IN)]
;
;   CHANGED:  1)  Changed location to ~/general_math directory and cleaned up
;                                                                   [05/15/2014   v1.0.1]
;             2)  Added keyword:  FLOG
;                                                                   [06/21/2014   v1.1.0]
;             3)  Added keywords:  NX, NY and
;                   changed how cut abscissa are determined and
;                   now calls eulermat.pro
;                                                                   [06/23/2014   v1.2.0]
;
;   NOTES:      
;               1)  See also IDL's documentation of INTERPOLATE.PRO and
;                     find_frac_indices.pro
;
;  REFERENCES:  
;               
;
;   CREATED:  07/16/2013
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  06/23/2014   v1.2.0
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

FUNCTION find_1d_cuts_2d_dist,fxy,x__in,y__in,X_0=x_0,Y_0=y_0,ANGLE=angle,FLOG=flog,$
                              NX=nx,NY=ny

;;----------------------------------------------------------------------------------------
;;  Define some constants and dummy variables
;;----------------------------------------------------------------------------------------
f              = !VALUES.F_NAN
d              = !VALUES.D_NAN
;;  Dummy error messages
bad_x__in_msg  = '[X,Y]__IN both must be [N]-element arrays, N=1,2,...'
bad_fxy_msg    = 'FXY must be an [N,N]-element array, N=1,2,...'
bad_numin_msg  = 'Incorrect number of inputs!'
;;  Create dummy return structure
tag_prefx      = ['X_','Y_']
tag_suffx      = ['1D_FXY','CUT_COORD','0','XY_COORD']
tags           = [tag_prefx[0]+tag_suffx,tag_prefx[1]+tag_suffx]
dumb1d         = REPLICATE(d,10L)
dumb2d         = [[dumb1d],[dumb1d]]
dummy          = CREATE_STRUCT(tags,dumb1d,dumb1d,d,dumb2d,dumb1d,dumb1d,d,dumb2d)
;;----------------------------------------------------------------------------------------
;;  Check input
;;----------------------------------------------------------------------------------------
test           = (N_PARAMS() NE 3)
IF (test) THEN BEGIN
  ;;  Must be 3 inputs supplied
  MESSAGE,bad_numin_msg[0],/INFORMATIONAL,/CONTINUE
  RETURN,dummy
ENDIF
test           = (N_ELEMENTS(x__in)  EQ 0) OR (N_ELEMENTS(y__in)  EQ 0) OR $
                 (N_ELEMENTS(x__in)  NE N_ELEMENTS(y__in))
IF (test) THEN BEGIN
  ;;  X__IN and Y__IN must have the same number of elements
  MESSAGE,bad_x__in_msg[0],/INFORMATIONAL,/CONTINUE
  RETURN,dummy
ENDIF
x_i            = REFORM(x__in)
y_i            = REFORM(y__in)
nvx            = N_ELEMENTS(x_i)
nvy            = N_ELEMENTS(y_i)
szf            = SIZE(fxy,/DIMENSIONS)
test           = (N_ELEMENTS(fxy) EQ 0) OR (N_ELEMENTS(szf) NE 2) OR $
                 (szf[0] NE N_ELEMENTS(x__in)) OR (szf[0] NE N_ELEMENTS(y__in))
IF (test) THEN BEGIN
  ;;  FXY must be an [N,N]-element array
  MESSAGE,bad_fxy_msg[0],/INFORMATIONAL,/CONTINUE
  RETURN,dummy
ENDIF
fv             = REFORM(fxy)
;;----------------------------------------------------------------------------------------
;;  Check keywords
;;----------------------------------------------------------------------------------------
IF (N_ELEMENTS(x_0)   NE 1) THEN x_o  = 0d0 ELSE x_o  = x_0[0]
IF (N_ELEMENTS(y_0)   NE 1) THEN y_o  = 0d0 ELSE y_o  = y_0[0]
IF (N_ELEMENTS(angle) NE 1) THEN ang  = 0d0 ELSE ang  = angle[0]
;;  Check FLOG
test           = (N_ELEMENTS(flog) EQ 0) OR ~KEYWORD_SET(flog)
IF (test[0])                THEN lnf  = 0b  ELSE lnf  = 1b
;; Make sure offsets and angle are finite
IF (FINITE(x_o)       NE 1) THEN x_o  = 0d0
IF (FINITE(y_o)       NE 1) THEN y_o  = 0d0
IF (FINITE(ang)       NE 1) THEN ang  = 0d0
;;  Check NX and NY
IF (N_ELEMENTS(nx)    NE 1) THEN xn   = nvx[0] ELSE xn   = nx[0]
IF (N_ELEMENTS(ny)    NE 1) THEN yn   = nvy[0] ELSE yn   = ny[0]
;;----------------------------------------------------------------------------------------
;;  Define XY ranges
;;----------------------------------------------------------------------------------------
xran           = [MIN(x_i,/NAN),MAX(x_i,/NAN)]*1.05
yran           = [MIN(y_i,/NAN),MAX(y_i,/NAN)]*1.05
;;----------------------------------------------------------------------------------------
;;  Define cut through f(x,y) at angle ø and offset {X_0, Y_0}
;;----------------------------------------------------------------------------------------
xzeros         = REPLICATE(0d0,xn[0])
yzeros         = REPLICATE(0d0,yn[0])
;;  Define dummy X-crosshairs at origin parallel to XY-axes
xxdum          = DINDGEN(xn[0])*(xran[1] - xran[0])/(xn[0] - 1L) + xran[0]
xydum          = DINDGEN(xn[0])*(yran[1] - yran[0])/(xn[0] - 1L) + yran[0]
xyzxdum        = [[xxdum],[xzeros],[xzeros]]
;xyzxdum        = [[xxdum],[xydum],[xzeros]]
;;  Define dummy Y-crosshairs at origin parallel to XY-axes
yxdum          = DINDGEN(yn[0])*(xran[1] - xran[0])/(yn[0] - 1L) + xran[0]
yydum          = DINDGEN(yn[0])*(yran[1] - yran[0])/(yn[0] - 1L) + yran[0]
xyzydum        = [[yzeros],[yydum],[yzeros]]
;xyzydum        = [[yxdum],[yydum],[yzeros]]
;;  Rotate lines by angle ø
rmat           = eulermat(0d0,ang[0],0d0,/DEG)
xyzxrot        = REFORM(rmat ## xyzxdum)
xyzyrot        = REFORM(rmat ## xyzydum)
;;  Shift by {X_0, Y_0}
xyzxproj       = xyzxrot
xyzyproj       = xyzyrot
xyzxproj[*,0] += x_o[0]
xyzxproj[*,1] += y_o[0]
xyzyproj[*,0] += x_o[0]
xyzyproj[*,1] += y_o[0]
;;  Find corresponding indices of f(x,y)
xyfindx        = find_frac_indices(x_i,y_i,xyzxproj[*,0],xyzxproj[*,1])
xyfindy        = find_frac_indices(x_i,y_i,xyzyproj[*,0],xyzyproj[*,1])
;;  Calculate values of f(x,y) along cut lines
IF (lnf[0]) THEN df_e = ALOG(fv) ELSE df_e = fv
df_ran         = [MIN(df_e,/NAN),MAX(df_e,/NAN)]
dfcut_x        = INTERPOLATE(df_e,xyfindx.X_IND,xyfindx.Y_IND,MISSING=d)
dfcut_y        = INTERPOLATE(df_e,xyfindy.X_IND,xyfindy.Y_IND,MISSING=d)
;;  Remove "bad" points due to extrapolation
testx          = (dfcut_x GT df_ran[1]) OR (dfcut_x LT df_ran[0])
testy          = (dfcut_y GT df_ran[1]) OR (dfcut_y LT df_ran[0])
badx           = WHERE(testx,bdx,COMPLEMENT=goodx,NCOMPLEMENT=gdx)
bady           = WHERE(testy,bdy,COMPLEMENT=goody,NCOMPLEMENT=gdy)
IF (bdx GT 0) THEN dfcut_x[badx] = d
IF (bdy GT 0) THEN dfcut_y[bady] = d
;;  Return to regular if in log-space
IF (lnf[0])   THEN dfcut_x = EXP(dfcut_x)
IF (lnf[0])   THEN dfcut_y = EXP(dfcut_y)
;;  Define [X,Y] projection coordinates
xy_proj_x      = [[xyzxproj[*,0]],[xyzxproj[*,1]]]
xy_proj_y      = [[xyzyproj[*,0]],[xyzyproj[*,1]]]
;xy_proj_y      = [[xyzyproj[*,1]],[xyzyproj[*,0]]]
;;  Define x-cut projection coordinates
x_proj         = xyzxproj[*,0]
;;  Define y-cut projection coordinates
y_proj         = xyzyproj[*,1]



;;;  Define slope and Y-intercept of cut
;slope          = TAN(ang[0]*!DPI/18d1)
;yintc          = y_o[0] - slope[0]*x_o[0]
;;;  Check to see if ANGLE = 90
;test00         = (ABS(ang[0]) EQ 0d0)
;test90         = (ABS(ang[0]) EQ 9d1)
;IF (test[0]) THEN BEGIN
;ENDIF ELSE BEGIN
;ENDELSE
;;;  Define new line from Y = m*X + b
;;xn             = nvx[0]
;xnew           = DINDGEN(xn)*(MAX(x_i,/NAN) - MIN(x_i,/NAN))/(xn - 1L) + MIN(x_i,/NAN)
;ynew           = slope[0]*xnew + yintc[0]
;;;  Find corresponding indices of f(x,y)
;xyfind         = find_frac_indices(x_i,y_i,xnew,ynew)
;;;  Calculate values of f(x,y) along cut line
;;df_e           = ALOG(fv)
;IF (lnf[0]) THEN df_e = ALOG(fv) ELSE df_e = fv
;df_ran         = [MIN(df_e,/NAN),MAX(df_e,/NAN)]
;dfcut_e        = INTERPOLATE(df_e,xyfind.X_IND,xyfind.Y_IND,MISSING=d)
;;dfcut_x        = EXP(dfcut_e)
;IF (lnf[0]) THEN dfcut_x = EXP(dfcut_e) ELSE dfcut_x = dfcut_e
;;;  Remove "bad" points due to extrapolation
;teste          = (dfcut_e GT df_ran[1]) OR (dfcut_e LT df_ran[0])
;bad            = WHERE(teste,bd,COMPLEMENT=good,NCOMPLEMENT=gd)
;IF (bd GT 0) THEN dfcut_x[bad] = d
;;;  Define [X,Y] projection coordinates
;xy_proj_x      = [[xnew],[ynew]]
;;;  Define x-cut projection coordinates
;x_proj         = xnew
;;;----------------------------------------------------------------------------------------
;;;  Define cut perpendicular to last
;;;----------------------------------------------------------------------------------------
;ang_p          = ang[0] - 9d1
;;;  Define slope and Y-intercept of cut
;slope_p        = TAN(ang_p[0]*!DPI/18d1)
;yintc_p        = y_o[0] - slope_p[0]*x_o[0]
;;;  Define new line from Y = m*X + b
;xnew_p         = ynew
;ynew_p         = slope_p[0]*xnew_p + yintc_p[0]
;;;  Find corresponding indices of f(x,y)
;xyfind_p       = find_frac_indices(x_i,y_i,xnew_p,ynew_p)
;;;  Calculate values of f(x,y) along cut line
;dfcut_ep       = INTERPOLATE(df_e,xyfind_p.X_IND,xyfind_p.Y_IND,MISSING=d)
;;dfcut_y        = EXP(dfcut_ep)
;IF (lnf[0]) THEN dfcut_y = EXP(dfcut_ep) ELSE dfcut_y = dfcut_ep
;;;  Remove "bad" points due to extrapolation
;teste          = (dfcut_ep GT df_ran[1]) OR (dfcut_ep LT df_ran[0])
;bad            = WHERE(teste,bd,COMPLEMENT=good,NCOMPLEMENT=gd)
;IF (bd GT 0) THEN dfcut_y[bad] = d
;;;  Define [X,Y] projection coordinates
;xy_proj_y      = [[xnew_p],[ynew_p]]
;;;  Define y-cut projection coordinates
;y_proj         = ynew_p
;;----------------------------------------------------------------------------------------
;;  Create return structure
;;----------------------------------------------------------------------------------------
tag_prefx      = ['X_','Y_']
tag_suffx      = ['1D_FXY','CUT_COORD','0','XY_COORD','XY_INDS']
tags           = [tag_prefx[0]+tag_suffx,tag_prefx[1]+tag_suffx]
struct         = CREATE_STRUCT(tags,dfcut_x,x_proj,x_o,xy_proj_x,xyfindx,$
                                    dfcut_y,y_proj,y_o,xy_proj_y,xyfindy)
;;----------------------------------------------------------------------------------------
;;  Return structure to user
;;----------------------------------------------------------------------------------------

RETURN,struct
END








