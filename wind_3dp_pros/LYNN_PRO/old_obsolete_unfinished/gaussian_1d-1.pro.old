;+
;*****************************************************************************************
;
;  FUNCTION :   gaussian_1d.pro
;  PURPOSE  :   Creates a one-dimensional Gaussian distribution from input parameters.
;                 The model function is defined as:
;                                               (X - Xo)^2
;                                            - ------------
;                                 Ao              2 ∆^2
;                   f(x) = --------------- e
;                           [2π∆^2]^(1/2)
;
;  CALLED BY:   
;               NA
;
;  CALLS:
;               gaussian_1d.pro
;
;  REQUIRES:    
;               NA
;
;  INPUT:
;               X        :  [N]-Element array of independent variable values
;               PARAM    :  [6]-Element array containing the following quantities:
;                             PARAM[0] = Normalization amplitude [Ao]
;                             PARAM[1] = X-offset [Xo]
;                             PARAM[2] = Standard Deviation [∆]
;                             PARAM[3] = *** Not Used Here ***
;                             PARAM[4] = *** Not Used Here ***
;                             PARAM[5] = *** Not Used Here ***
;
;  EXAMPLES:    
;               NA
;
;  KEYWORDS:    
;               TOT_AMP  :  If set, routine assumes that PARAM[0] corresponds to the
;                             total peak Gaussian amplitude, i.e., = Ao/[2π∆^2]^(1/2)
;                             => routine determines Ao and re-calls itself
;                             [Default = FALSE]
;
;   CHANGED:  1)  Continued to write routine
;                                                                   [01/22/2014   v1.0.0]
;
;   NOTES:      
;               1)  Fo = Ao * [2π∆^2]^(-1/2)
;
;   CREATED:  01/21/2014
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  01/22/2014   v1.0.0
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

FUNCTION gaussian_1d,x,param,TOT_AMP=tot_amp

;;----------------------------------------------------------------------------------------
;;  Define some constants and dummy variables
;;----------------------------------------------------------------------------------------
f              = !VALUES.F_NAN
d              = !VALUES.D_NAN
;;----------------------------------------------------------------------------------------
;;  Check input
;;----------------------------------------------------------------------------------------
IF (N_PARAMS() LT 2) THEN BEGIN
  ;;  no input???
  RETURN,0
ENDIF

np             = N_ELEMENTS(param)
IF (np LT 3) THEN BEGIN
  ;;  bad input???
  RETURN,0
ENDIF
;;----------------------------------------------------------------------------------------
;;  Calculate distribution
;;----------------------------------------------------------------------------------------
CASE np OF
  3L   :  RETURN,gaussian_1d(x,[param,0d0,0d0,0d0],TOT_AMP=tot_amp)
  4L   :  RETURN,gaussian_1d(x,[param,0d0,0d0],TOT_AMP=tot_amp)
  5L   :  RETURN,gaussian_1d(x,[param,0d0],TOT_AMP=tot_amp)
  6L   :  BEGIN
    ;;------------------------------------------------------------------------------------
    ;;  Correct input  => Calculate distribution
    ;;------------------------------------------------------------------------------------
    ;;  Check if user knows Ao or Fo
    test           = (N_ELEMENTS(tot_amp) NE 0) AND KEYWORD_SET(tot_amp)
    IF (test) THEN BEGIN
      ;;  User only knows Fo {= Ao/[2π∆^2]^(1/2)}, so calculate Ao and re-call
      fo    = param[0]
      var   = param[2]^2
      Ao    = fo[0]*SQRT(2d0*!DPI*var[0])
      ;;  Redefine PARAM
      param = [Ao[0],param[1:2],0d0,0d0,0d0]
      RETURN,gaussian_1d(x,param)
    ENDIF
    ;;  Define variance, = ∆^2
    var            = param[2]^2
    ;;  Define amplitude, = Ao/[2π∆^2]^(1/2)
    amp            = param[0]/SQRT(2d0*!DPI*var[0])
    ;;  Define exponent
    expn           = (x - param[1])^2      ;;  numerator of exponent, = (X - Xo)^2
    expd           = 2d0*var[0]            ;;  denominator of exponent, = 2 ∆^2
    expo           = -1d0*expn/expd[0]
    ;;  Define Gaussian
    df             = amp[0]*EXP(expo)
  END
  ELSE :  BEGIN
    ;;  More than 5 elements in param
    RETURN,gaussian_1d(x,param[0L:4L],TOT_AMP=tot_amp)
  END
ENDCASE
;;----------------------------------------------------------------------------------------
;;  Return to user
;;----------------------------------------------------------------------------------------

RETURN,df
END

