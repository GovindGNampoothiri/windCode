;+
;*****************************************************************************************
;
;  FUNCTION :   lbw_perturb_model_func.pro
;  PURPOSE  :   This routine takes the model fit results from a routine like
;                 wrapper_multi_func_fit.pro and then perturbs the fit parameters,
;                 PARAM, using the uncertainties in those parameters, DPARM, to create
;                 a range of possible fit lines evaluated at abscissa values, XX.  Thus,
;                 A --> { A - ∂A, A, A + ∂A } and so on, providing 3^4 (or 81) possible
;                 parameter values to evaluate.  This is useful for creating ranges for
;                 predictive or constraint purposes.
;
;  CALLED BY:   
;               NA
;
;  INCLUDES:
;               NA
;
;  CALLS:
;               is_a_number.pro
;
;  REQUIRES:    
;               1)  UMN Modified Wind/3DP IDL Libraries
;
;  INPUT:
;               X         :  [N]-Element [float/double] array of independent variable
;                              values or abscissa, where F_j = F(X_j)
;               PARAM     :  [4]-Element [float/double] array containing the following
;                              model fit parameters, {A,B,C,D}, for the model functions
;                              (see KEYWORDS section below) defined as:
;                                PARAM[0] = A
;                                PARAM[1] = B
;                                PARAM[2] = C
;                                PARAM[3] = D
;               DPARM     :  [4]-Element [float/double] array defining the uncertainties
;                              for each fit element in PARAM
;
;  EXAMPLES:    
;               [calling sequence]
;               test = lbw_perturb_model_func(xx, param, dparm [,FUNC=func])
;
;               ;;  Example usage
;               xx             = [1d-2,1d0,1d1,1d2,1d4,1d7]
;               param          = [3.972d0,0.536d0,1.859d0,0d0]
;               dparm          = [0.055d0,0.007d0,0.047d0,0d0]
;               func           = 1
;               test           = lbw_perturb_model_func(xx,param,dparm,FUNC=func)
;               HELP, test
;                 TEST            DOUBLE    = Array[6, 3, 3, 3, 3]
;
;  KEYWORDS:    
;               FUNC      :  Scalar [integer] specifying the type of function to use
;                              [Default  :  1]
;                              1  :  F(X) = A X^(B) + C
;                              2  :  F(X) = A e^(B X) + C
;                              3  :  F(X) = A + B Log_{e} |X^C|
;                              4  :  F(X) = A X^(B) e^(C X) + D
;                              5  :  F(X) = A B^(X) + C
;                              6  :  F(X) = A B^(C X) + D
;                              7  :  F(X) = ( A + B X )^(-1)
;                              8  :  F(X) = ( A B^(X) + C )^(-1)
;                              9  :  F(X) = A X^(B) ( e^(C X) + D )^(-1)
;                             10  :  F(X) = A + B Log_{10} |X| + C (Log_{10} |X|)^2
;                             11  :  F(X) = A X^(B) e^(C X) e^(D X)
;
;   CHANGED:  1)  Fixed a bug where the number of elements of the input X exceeded the
;                   signed integer limit for the FOR loops in the definitions of the
;                   outputs
;                                                                  [12/11/2015   v1.0.1]
;             2)  Removed the FOR loop over the N elements of X and changed CASE
;                   statement
;                                                                  [12/11/2015   v1.0.2]
;
;   NOTES:      
;               1)  Follow input specifications above...
;
;  REFERENCES:  
;               NA
;
;   CREATED:  07/25/2015
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  12/11/2015   v1.0.2
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

FUNCTION lbw_perturb_model_func,x,param,dparm,FUNC=func

;;  Let IDL know that the following are functions
FORWARD_FUNCTION is_a_number
;;----------------------------------------------------------------------------------------
;;  Define some constant, dummy, and default variables
;;----------------------------------------------------------------------------------------
f              = !VALUES.F_NAN
d              = !VALUES.D_NAN
def_func       = 1
func_mnmx      = [1,11]
ones           = [-1,0,1]
;;  Dummy error messages
no_inpt_msg    = 'User must supply X [numeric], PARAM [numeric], and DPARM [numeric] inputs...'
badinpt_msg    = 'PARAM and DPARM must be [4]-element arrays...'
;;----------------------------------------------------------------------------------------
;;  Check input
;;----------------------------------------------------------------------------------------
test           = (N_PARAMS() LT 3) OR (is_a_number(x,/NOMSSG) EQ 0) OR                 $
                 (is_a_number(param,/NOMSSG) EQ 0) OR (is_a_number(dparm,/NOMSSG) EQ 0)
IF (test) THEN BEGIN
  MESSAGE,no_inpt_msg[0],/INFORMATIONAL,/CONTINUE
  RETURN,0b
ENDIF
;;  Check format
test           = ((N_ELEMENTS(param) LT 4) OR (N_ELEMENTS(dparm) LT 4)) OR $
                  (N_ELEMENTS(param) NE N_ELEMENTS(dparm))
IF (test) THEN BEGIN
  MESSAGE,badinpt_msg[0],/INFORMATIONAL,/CONTINUE
  RETURN,0b
ENDIF
;;----------------------------------------------------------------------------------------
;;  Define new parameters
;;----------------------------------------------------------------------------------------
xx             = REFORM(x)
pp             = REFORM(param)
dp             = REFORM(dparm)
nx             = N_ELEMENTS(xx)
nones          = REPLICATE(1d0,nx[0])
a_vary         = pp[0] + ones*dp[0]     ;;  i.e., [A - ∂A, A, A + ∂A]
b_vary         = pp[1] + ones*dp[1]
c_vary         = pp[2] + ones*dp[2]
d_vary         = pp[3] + ones*dp[3]
;v_parm         = [[pp - dp],[pp],[pp + dp]]
;all_vary       = DBLARR(4L,3L,3L,3L,3L)
;temp           = DBLARR(nx[0],4L)
;all_vary       = DBLARR(nx[0],4L,3L,3L,3L,3L)
;FOR i=0, 2 DO BEGIN              ;;  A index
;  a_o                 = a_vary[i]
;  FOR j=0, 2 DO BEGIN            ;;  B index
;    b_o                 = b_vary[j]
;    FOR k=0, 2 DO BEGIN          ;;  C index
;      c_o                 = c_vary[k]
;      FOR l=0, 2 DO BEGIN        ;;  D index
;        d_o                   = d_vary[l]
;        temp[*,0]             = a_o[0]
;        temp[*,1]             = b_o[0]
;        temp[*,2]             = c_o[0]
;        temp[*,3]             = d_o[0]
;;        temp                  = nones # ([a_o[0],b_o[0],c_o[0],d_o[0]])
;        all_vary[*,*,i,j,k,l] = temp
;;        all_vary[*,i,j,k,l] = [a_o[0],b_o[0],c_o[0],d_o[0]]
;      ENDFOR
;    ENDFOR
;  ENDFOR
;ENDFOR
;a_vary_4d      = REFORM(all_vary[*,0,*,*,*,*])     ;;  [N,3,3,3,3]-Element arrays
;b_vary_4d      = REFORM(all_vary[*,1,*,*,*,*])
;c_vary_4d      = REFORM(all_vary[*,2,*,*,*,*])
;d_vary_4d      = REFORM(all_vary[*,3,*,*,*,*])
;a_vary_4d      = REFORM(all_vary[0,*,*,*,*])
;b_vary_4d      = REFORM(all_vary[1,*,*,*,*])
;c_vary_4d      = REFORM(all_vary[2,*,*,*,*])
;d_vary_4d      = REFORM(all_vary[3,*,*,*,*])
;;----------------------------------------------------------------------------------------
;;  Check keywords
;;----------------------------------------------------------------------------------------
;;  Check FUNC
test           = (N_ELEMENTS(func) EQ 0) OR (is_a_number(func,/NOMSSG) EQ 0)
IF (test) THEN fitf = def_func[0] ELSE fitf = (FIX(func[0]) > func_mnmx[0]) < func_mnmx[1]
;;----------------------------------------------------------------------------------------
;;  Calculate result
;;----------------------------------------------------------------------------------------
ff             = DBLARR(nx[0],3L,3L,3L,3L)
CASE fitf[0] OF
  1    : BEGIN
    ;;  Y = A X^(B) + C
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
;          FOR l=0, 2 DO BEGIN        ;;  D index
;            d_o                   = d_vary[l]
            temp                  = a_o[0]*(xx^(b_o[0])) + c_o[0]
            ff[*,i,j,k,0]         = temp
            ff[*,i,j,k,1]         = temp
            ff[*,i,j,k,2]         = temp
;          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    ff            = a_vary_4d*(xx^(b_vary_4d)) + c_vary_4d
;    FOR n=0, nx[0] - 1L DO BEGIN
;      ff[n,*,*,*,*] = a_vary_4d*(xx[n]^(b_vary_4d)) + c_vary_4d
;    ENDFOR
  END
  2    : BEGIN
    ;;  Y = A e^(B X) + C
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
;          FOR l=0, 2 DO BEGIN        ;;  D index
;            d_o                   = d_vary[l]
            temp                  = a_o[0]*EXP(b_o[0]*xx) + c_o[0]
            ff[*,i,j,k,0]         = temp
            ff[*,i,j,k,1]         = temp
            ff[*,i,j,k,2]         = temp
;          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    ff            = a_vary_4d*EXP(b_vary_4d*xx) + c_vary_4d
;    FOR n=0, nx[0] - 1L DO BEGIN
;      ff[n,*,*,*,*] = a_vary_4d*EXP(b_vary_4d*xx[n]) + c_vary_4d
;    ENDFOR
  END
  3    : BEGIN
    ;;  Y = A + B Log_{e} |X^C|
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
;          FOR l=0, 2 DO BEGIN        ;;  D index
;            d_o                   = d_vary[l]
            temp                  = a_o[0] + b_o[0]*ALOG(ABS(xx^(c_o[0])))
            ff[*,i,j,k,0]         = temp
            ff[*,i,j,k,1]         = temp
            ff[*,i,j,k,2]         = temp
;          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    term0         = ALOG(ABS(xx^(c_vary_4d)))
;    ff            = a_vary_4d + b_vary_4d*term0
;    FOR n=0, nx[0] - 1L DO BEGIN
;      term0         = ALOG(ABS(xx[n]^(c_vary_4d)))
;      ff[n,*,*,*,*] = a_vary_4d + b_vary_4d*term0
;    ENDFOR
  END
  4    : BEGIN
    ;;  Y = A X^(B) e^(C X) + D
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
          FOR l=0, 2 DO BEGIN        ;;  D index
            d_o                   = d_vary[l]
            ff[*,i,j,k,l]         = a_o[0]*(xx^(b_o[0]))*EXP(c_o[0]*xx) + d_o[0]
          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    ff            = a_vary_4d*(xx^(b_vary_4d))*EXP(c_vary_4d*xx) + d_vary_4d
;    FOR n=0, nx[0] - 1L DO BEGIN
;      ff[n,*,*,*,*] = a_vary_4d*(xx[n]^(b_vary_4d))*EXP(c_vary_4d*xx[n]) + d_vary_4d
;    ENDFOR
  END
  5    : BEGIN
    ;;  Y = A B^(X) + C
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
;          FOR l=0, 2 DO BEGIN        ;;  D index
;            d_o                   = d_vary[l]
            temp                  = a_o[0]*(b_o[0]^(xx)) + c_o[0]
            ff[*,i,j,k,0]         = temp
            ff[*,i,j,k,1]         = temp
            ff[*,i,j,k,2]         = temp
;          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    ff            = a_vary_4d*(b_vary_4d^(xx)) + c_vary_4d
;    FOR n=0, nx[0] - 1L DO BEGIN
;      ff[n,*,*,*,*] = a_vary_4d*(b_vary_4d^(xx[n])) + c_vary_4d
;    ENDFOR
  END
  6    : BEGIN
    ;;  Y = A B^(C X) + D
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
          FOR l=0, 2 DO BEGIN        ;;  D index
            d_o                   = d_vary[l]
;            temp                  = a_o[0]*(b_o[0]^(c_o[0]*xx)) + d_o[0]
;            ff[*,i,j,k,l]         = temp
            ff[*,i,j,k,l]         = a_o[0]*(b_o[0]^(c_o[0]*xx)) + d_o[0]
          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    ff            = a_vary_4d*(b_vary_4d^(c_vary_4d*xx)) + d_vary_4d
;    FOR n=0, nx[0] - 1L DO BEGIN
;      ff[n,*,*,*,*] = a_vary_4d*(b_vary_4d^(c_vary_4d*xx[n])) + d_vary_4d
;    ENDFOR
  END
  7    : BEGIN
    ;;  Y = ( A + B X )^(-1)
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
;          FOR l=0, 2 DO BEGIN        ;;  D index
;            d_o                   = d_vary[l]
            temp                  = 1d0/(a_o[0] + b_o[0]*xx)
            ff[*,i,j,k,0]         = temp
            ff[*,i,j,k,1]         = temp
            ff[*,i,j,k,2]         = temp
;          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    term0         = a_vary_4d + b_vary_4d*xx
;    ff            = 1d0/term0
;    FOR n=0, nx[0] - 1L DO BEGIN
;      term0         = a_vary_4d + b_vary_4d*xx[n]
;      ff[n,*,*,*,*] = 1d0/term0
;    ENDFOR
  END
  8    : BEGIN
    ;;  Y = ( A B^(X) + C )^(-1)
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
;          FOR l=0, 2 DO BEGIN        ;;  D index
;            d_o                   = d_vary[l]
            temp                  = 1d0/( a_o[0]*(b_o[0]^(xx)) + c_o[0] )
            ff[*,i,j,k,0]         = temp
            ff[*,i,j,k,1]         = temp
            ff[*,i,j,k,2]         = temp
;          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    term0         = a_vary_4d*(b_vary_4d^(xx)) + c_vary_4d
;    ff            = 1d0/term0
;    FOR n=0, nx[0] - 1L DO BEGIN
;      term0         = a_vary_4d*(b_vary_4d^(xx[n])) + c_vary_4d
;      ff[n,*,*,*,*] = 1d0/term0
;    ENDFOR
  END
  9    : BEGIN
    ;;  Y = A X^(B) ( e^(C X) + D )^(-1)
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
          FOR l=0, 2 DO BEGIN        ;;  D index
            d_o                   = d_vary[l]
;            temp                  = a_o[0]*(xx^(b_o[0]))/( EXP(c_o[0]*xx) + d_o[0] )
;            ff[*,i,j,k,l]         = temp
            ff[*,i,j,k,l]         = a_o[0]*(xx^(b_o[0]))/( EXP(c_o[0]*xx) + d_o[0] )
          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    term0         = EXP(c_vary_4d*xx) + d_vary_4d
;    ff            = a_vary_4d*(xx^(b_vary_4d))/term0
;    FOR n=0, nx[0] - 1L DO BEGIN
;      term0         = EXP(c_vary_4d*xx[n]) + d_vary_4d
;      ff[n,*,*,*,*] = a_vary_4d*(xx[n]^(b_vary_4d))/term0
;    ENDFOR
  END
  10   : BEGIN
    ;;  Y = A + B Log_{10} |X| + C (Log_{10} |X|)^2
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
;          FOR l=0, 2 DO BEGIN        ;;  D index
;            d_o                   = d_vary[l]
            temp                  = a_o[0] + b_o[0]*ALOG10(ABS(xx)) + c_o[0]*(ALOG10(ABS(xx))^2)
            ff[*,i,j,k,0]         = temp
            ff[*,i,j,k,1]         = temp
            ff[*,i,j,k,2]         = temp
;          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    term0         = a_vary_4d + b_vary_4d*ALOG10(ABS(xx))
;    term1         = c_vary_4d*(ALOG10(ABS(xx))^2)
;    ff            = term0 + term1
;    FOR n=0, nx[0] - 1L DO BEGIN
;      term0         = a_vary_4d + b_vary_4d*ALOG10(ABS(xx[n]))
;      term1         = c_vary_4d*(ALOG10(ABS(xx[n]))^2)
;      ff[n,*,*,*,*] = term0 + term1
;    ENDFOR
  END
  11   : BEGIN
    ;;  Y = A X^(B) e^(C X) e^(D X)
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
          FOR l=0, 2 DO BEGIN        ;;  D index
            d_o                   = d_vary[l]
            ff[*,i,j,k,l]         = a_o[0]*(xx^(b_o[0]))*EXP(c_o[0]*xx)*EXP(d_o[0]*xx)
          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    term0         = a_vary_4d*(xx^(b_vary_4d))
;    term1         = EXP(c_vary_4d*xx)*EXP(d_vary_4d*xx)
;    ff            = term0*term1
;    FOR n=0, nx[0] - 1L DO BEGIN
;      term0         = a_vary_4d*(xx[n]^(b_vary_4d))
;      term1         = EXP(c_vary_4d*xx[n])*EXP(d_vary_4d*xx[n])
;      ff[n,*,*,*,*] = term0*term1
;    ENDFOR
  END
  ELSE : BEGIN
    ;;  Use default:  Y = A X^(B) + C
    FOR i=0, 2 DO BEGIN              ;;  A index
      a_o                 = a_vary[i]
      FOR j=0, 2 DO BEGIN            ;;  B index
        b_o                 = b_vary[j]
        FOR k=0, 2 DO BEGIN          ;;  C index
          c_o                 = c_vary[k]
;          FOR l=0, 2 DO BEGIN        ;;  D index
;            d_o                   = d_vary[l]
            temp                  = a_o[0]*(xx^(b_o[0])) + c_o[0]
            ff[*,i,j,k,0]         = temp
            ff[*,i,j,k,1]         = temp
            ff[*,i,j,k,2]         = temp
;          ENDFOR
        ENDFOR
      ENDFOR
    ENDFOR
;    ff            = a_vary_4d*(xx^(b_vary_4d)) + c_vary_4d
;    FOR n=0, nx[0] - 1L DO BEGIN
;      ff[n,*,*,*,*] = a_vary_4d*(xx[n]^(b_vary_4d)) + c_vary_4d
;    ENDFOR
  END
ENDCASE
;;----------------------------------------------------------------------------------------
;;  Return to user
;;----------------------------------------------------------------------------------------

RETURN,ff
END


;;;  Benchmark tests (old vs. new)
;nf             = 11L
;all_funcs      = LINDGEN(nf[0]) + 1
;dt0            = DINDGEN(nf[0],2L)
;diffs          = DINDGEN(nf[0],2L)
;FOR j=0L, nf[0] - 1L DO BEGIN                                         $
;  ex_start = SYSTIME(1)                                             & $
;  func     = all_funcs[j]                                           & $
;  test     = lbw_perturb_model_func(xx,param,dparm,FUNC=func)       & $
;  dt0[j,0] = SYSTIME(1) - ex_start[0]                               & $
;  ex_start = SYSTIME(1)                                             & $
;  func     = all_funcs[j]                                           & $
;  test2    = temp_lbw_perturb_model_func(xx,param,dparm,FUNC=func)  & $
;  dt0[j,1] = SYSTIME(1) - ex_start[0]                               & $
;  diffs[j,*] = minmax(ABS(test - test2))
;
;
;out_pre        = ';;  j = '
;FOR j=0L, nf[0] - 1L DO BEGIN                                           $
;  dt   = ABS(MAX(dt0[j,*],/NAN) - MIN(dt0[j,*],/NAN))                 & $
;  jstr = out_pre[0]+STRING(j[0],FORMAT='(I2.2)')                      & $
;  dtss = STRTRIM(STRING(REFORM(dt0[j,*]),FORMAT='(f25.8)'),2)         & $
;  dt12 = STRTRIM(STRING(dt[0],FORMAT='(f25.8)'),2)                    & $
;  dtrat = STRTRIM(STRING(dt[0]/dt0[j,0]*1d2,FORMAT='(f25.7)'),2)+' %' & $
;  dtstr = ',  ∆t_1 = '+dtss[0]+',  ∆t_2 = '+dtss[1]                   & $
;  dtstr = dtstr[0]+',  ∆t_12 = '+dt12[0]+',  ∆t_12/∆t_1 = '+dtrat[0]  & $
;  dfstr = STRTRIM(STRING(REFORM(diffs[j,*]),FORMAT='(f25.8)'),2)      & $
;  dfout = jstr[0]+',  ∆f_min = '+dfstr[0]+',  ∆f_max = '+dfstr[1]     & $
;  dtout = dfout[0]+dtstr[0]                                           & $
;  PRINT,dtout[0]
;;;  j = 00,  ∆f_min = 0.00000000,  ∆f_max = 0.00000000,  ∆t_1 = 0.49463010,  ∆t_2 = 0.13168001,  ∆t_12 = 0.36295009,  ∆t_12/∆t_1 = 73.3780835 %
;;;  j = 01,  ∆f_min = 0.00000000,  ∆f_max = 0.00000000,  ∆t_1 = 0.26742792,  ∆t_2 = 0.20048213,  ∆t_12 = 0.06694579,  ∆t_12/∆t_1 = 25.0332093 %
;;;  j = 02,  ∆f_min = 0.00000000,  ∆f_max = 0.00000000,  ∆t_1 = 0.63439107,  ∆t_2 = 0.25178885,  ∆t_12 = 0.38260221,  ∆t_12/∆t_1 = 60.3101515 %
;;;  j = 03,  ∆f_min = 0.00000000,  ∆f_max = 0.00000000,  ∆t_1 = 0.63672209,  ∆t_2 = 0.27795291,  ∆t_12 = 0.35876918,  ∆t_12/∆t_1 = 56.3462750 %
;;;  j = 04,  ∆f_min = 0.00000000,  ∆f_max = 0.00000000,  ∆t_1 = 0.72634912,  ∆t_2 = 0.13913298,  ∆t_12 = 0.58721614,  ∆t_12/∆t_1 = 80.8448894 %
;;;  j = 05,  ∆f_min = 0.00000000,  ∆f_max = 0.00000000,  ∆t_1 = 0.75641704,  ∆t_2 = 0.24457693,  ∆t_12 = 0.51184011,  ∆t_12/∆t_1 = 67.6663905 %
;;;  j = 06,  ∆f_min = 0.00000000,  ∆f_max = 0.00000000,  ∆t_1 = 0.25899386,  ∆t_2 = 0.12437105,  ∆t_12 = 0.13462281,  ∆t_12/∆t_1 = 51.9791512 %
;;;  j = 07,  ∆f_min = 0.00000000,  ∆f_max = 0.00000000,  ∆t_1 = 0.81815004,  ∆t_2 = 0.14787197,  ∆t_12 = 0.67027807,  ∆t_12/∆t_1 = 81.9260572 %
;;;  j = 08,  ∆f_min = 0.00000000,  ∆f_max = 0.00000000,  ∆t_1 = 0.67470217,  ∆t_2 = 0.27793813,  ∆t_12 = 0.39676404,  ∆t_12/∆t_1 = 58.8058049 %
;;;  j = 09,  ∆f_min = 0.00000000,  ∆f_max = 0.00000000,  ∆t_1 = 0.29755402,  ∆t_2 = 0.17516398,  ∆t_12 = 0.12239003,  ∆t_12/∆t_1 = 41.1320383 %
;;;  j = 10,  ∆f_min = 0.00000000,  ∆f_max = 0.00000000,  ∆t_1 = 0.75683308,  ∆t_2 = 0.32717705,  ∆t_12 = 0.42965603,  ∆t_12/∆t_1 = 56.7702499 %
