;+
;*****************************************************************************************
;
;  FUNCTION :   start_umn_3dp.pro
;  PURPOSE  :   Start up routine to initialize UMN versions of Berkeley SSL 
;                 Wind/3DP software upon starting IDL.
;
;  CALLED BY:   
;               NA
;
;  CALLS:
;               umn_graphics_config.pro
;               wind_3dp_umn_init.pro
;               plot3d_options.pro
;               TMlib_wrapper.pro
;
;  REQUIRES:    
;               UMN Edited 3DP Libraries
;
;  INPUT:
;               NA
;
;  EXAMPLES:    
;               @start_umn_3dp
;
;  KEYWORDS:    
;               NA
;
;   CHANGED:  1)  Changed compliation list                         [09/17/2009   v1.0.1]
;             2)  Changed compliation list                         [09/18/2009   v1.0.2]
;             3)  Added plot3d.pro options by calling plot3d_options.pro with default
;                   options already chosen                         [09/18/2009   v1.0.3]
;             4)  Changed compliation list                         [09/21/2009   v1.0.4]
;             5)  Added a device call for default formats          [09/21/2009   v1.0.5]
;             6)  Changed compliation list                         [09/24/2009   v1.0.6]
;             7)  Added extra option for plot3d_options.pro        [09/25/2009   v1.0.7]
;             8)  Added extra option for TMlib Client Software compilation which acts
;                   similar to calling "sidl" from command prompt instead of "uidl"
;                                                                  [10/22/2009   v1.1.0]
;             9)  No longer compiles routines on entry             [06/16/2011   v1.2.0]
;            10)  Cleaned up, changed path specification, and added error handling for
;                   TMlib_wrapper.pro batch call
;                                                                  [08/23/2012   v1.3.0]
;            11)  Now calls umn_graphics_config.pro to initialize graphics
;                                                                  [08/27/2012   v1.4.0]
;            12)  Now calls get_os_slash.pro to increase portability to other OS's
;                                                                  [08/07/2013   v1.5.0]
;            13)  Fixed a bug [i.e., removed DELVAR,slash]
;                                                                  [08/08/2013   v1.5.1]
;            14)  Forgot to remove hard-coded slash in tdir definition
;                                                                  [08/26/2013   v1.5.2]
;
;   NOTES:      
;               0)  Still in production...
;               1)  This routine can be called in 2 ways:
;                   1  :  using bash functions in your .bash_profile to source either
;                           umn3dp_start or umn3dp_start_64
;                   2  :  after starting IDL, type @start_umn_3dp.pro
;                           Note:  if IDL cannot find this routine, you can define the
;                                    path to it, e.g., for me it would be
;               IDL> @/Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/start_umn_3dp.pro
;               3)  Of the above 2 methods, I would recommend method 1 if you have a
;                   Unix or Linux machine because then you need only type in 4 or 6
;                   characters to start IDL and reset all the paths accordingly.  Once
;                   you exit IDL, the function in your .bash_profile will reset all your
;                   environment variables accordingly by sourcing your .bash_profile.
;               4)  See also:  umn3dp_start, umn3dp_start_64, and setup_wind3dp_bash
;
;   CREATED:  09/16/2009
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  08/26/2013   v1.5.2
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

;;----------------------------------------------------------------------------------------
;;  Define some constants and dummy variables
;;----------------------------------------------------------------------------------------
slash          = get_os_slash()         ;;  '/' for Unix, '\' for Windows
!PROMPT        = 'UMN> '                ;;  change prompt
;;----------------------------------------------------------------------------------------
;;  Initialize Graphics
;;----------------------------------------------------------------------------------------
umn_graphics_config

vers           = !VERSION.OS_FAMILY
mdir           = FILE_EXPAND_PATH('')
;;  Check for trailing '/'
ll             = STRMID(mdir, STRLEN(mdir) - 1L,1L)
test_ll        = (ll[0] NE slash[0])
IF (test_ll) THEN mdir = mdir[0]+slash[0]
;;----------------------------------------------------------------------------------------
;;  Reset IDL Path
;;----------------------------------------------------------------------------------------
tdir           = FILE_EXPAND_PATH('.'+slash[0]+'wind_3dp_pros'+slash[0])
t_path         = EXPAND_PATH('+'+tdir[0],/ARRAY)
gposi          = STRPOS(t_path,'wind_data_dir') GE 0
bad            = WHERE(gposi,bd,COMPLEMENT=good,NCOMPLEMENT=gd)
t_path         = STRJOIN(t_path[good],/SINGLE)

;;  Update path to include Wind/3DP libraries
;;  Get current path
idlpath        = GETENV('IDL_PATH')
;;  Check to see if user sourced setup_wind3dp_bash
lynnidl        = GETENV('IDL_LYNN_PRO_DIR')
test           = (lynnidl[0] EQ '')
;;  Prefix current path with UMN Modified Wind/3DP IDL Library paths
IF (test) THEN new_path = t_path[0]+':'+idlpath[0]
IF (test) THEN SETENV,'IDL_PATH='+new_path[0]
;;  Clean up
DELVAR,t_path,idlpath,gposi,bad,good,bd,gd
;;----------------------------------------------------------------------------------------
;;  Initialize !wind3dp_umn system variable
;;----------------------------------------------------------------------------------------
wind_3dp_umn_init
;;----------------------------------------------------------------------------------------
;;  Set up plot3d.pro options
;;----------------------------------------------------------------------------------------
;;  Default to a Hammer-Aitoff Projection
plot3d_options,MAP='ham',LOG =1,TRIANGULATE=1,COMPRESS=1,GRID=[30,30]
;;----------------------------------------------------------------------------------------
;;  Check for TMlib_wrapper
;;----------------------------------------------------------------------------------------
test           = slash[0]+'Applications'+slash[0]+'STEREO'+slash[0]+'TMlib_Client'+$
                 slash[0]+'lib'+slash[0]+'idl'+slash[0]
tdir           = FILE_EXPAND_PATH(test)
test           = FILE_TEST(tdir,/DIRECTORY)
IF (test EQ 0) THEN STOP
@TMlib_wrapper
;;  Clean up
DELVAR,tmfile,old_dir,test,tdir



