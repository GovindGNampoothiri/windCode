;+
;*****************************************************************************************
;
;  FUNCTION :   themis_esa_pad_template.pro
;  PURPOSE  :   Create a dummy pitch-angle distribution (PAD) structure formatted for
;                 the themis_esa_pad.pro routine.
;
;  CALLED BY:   
;               themis_esa_pad.pro
;
;  CALLS:
;               test_themis_esa_struc_format.pro
;
;  REQUIRES:    
;               1)  THEMIS TDAS IDL libraries and UMN Modified Wind/3DP IDL Libraries
;
;  INPUT:
;               DAT    :  Scalar structure associated with a known THEMIS ESA data
;                           structure
;                           [see get_th?_pe%$.pro, ? = a-f, % = i,e, $ = b,f,r]
;
;  EXAMPLES:    
;               
;
;  KEYWORDS:    
;               ESTEPS :  [2]-Element array specifying the first and last energy bins
;                           to keep in the returned structure
;                           [Default = [0,DAT.NENERGY - 1L]]
;               NUM_PA :  Scalar defining the number of pitch-angle bins to create
;                           [Default = 8]
;
;   CHANGED:  1)  NA [MM/DD/YYYY   v1.0.0]
;
;   NOTES:      
;               1)  The important structure tags are those used by my_padplot_both.pro
;                     and thm_convert_esa_units_lbwiii.pro
;               2)  User should not call this routine
;
;   CREATED:  08/15/2012
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  08/15/2012   v1.0.0
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

FUNCTION themis_esa_pad_template,dat,NUM_PA=num_pa,ESTEPS=esteps

;;----------------------------------------------------------------------------------------
;; => Define some constants and dummy variables
;;----------------------------------------------------------------------------------------
f              = !VALUES.F_NAN  ;; dummy scalar [float]
d              = !VALUES.D_NAN  ;; dummy scalar [double]
s              = ''             ;; dummy scalar [string]
i              = 0              ;; dummy scalar [integer]
b              = 0b             ;; dummy scalar [byte]
;; => Dummy error messages
notstr_mssg    = 'Must be an IDL structure...'
badstr_themis  = 'Not an appropriate THEMIS ESA structure...'
;;----------------------------------------------------------------------------------------
;; => Check keywords
;;----------------------------------------------------------------------------------------
;; Define default # of energy and solid angle bins
n_e            = 32L
enbins         = [0L,n_e - 1L]
n_a            = 88L
IF ~KEYWORD_SET(num_pa) THEN pang  = 8 ELSE pang  = (num_pa[0] < n_a[0]/3L) > 1L
IF KEYWORD_SET(esteps) THEN BEGIN
  test = (N_ELEMENTS(esteps) NE 2L)
  IF (test) THEN nener = enbins ELSE nener = esteps[SORT(esteps)]
ENDIF ELSE BEGIN
  nener = enbins
ENDELSE
;; Redefine default # of energy and solid angle bins
n_e            = nener[1] - nener[0] + 1L  ;; # of energy bins on output
n_a            = pang[0]                   ;; # of pitch-angle bins on output
;; Define dummy arrays for structure
f_32x88        = REPLICATE(f,n_e,n_a)
d_32x88        = REPLICATE(d,n_e,n_a)
i_32x88        = REPLICATE(i,n_e,n_a)
f_3            = REPLICATE(f,3L)
d_3            = REPLICATE(d,3L)

;; Define original ESA structure tags
old_tags       = ['PROJECT_NAME','SPACECRAFT','DATA_NAME','APID','UNITS_NAME',        $
                  'UNITS_PROCEDURE','VALID','TIME','END_TIME','DELTA_T','INTEG_T',    $
                  'DT_ARR','CONFIG1','CONFIG2','AN_IND','EN_IND','MODE','NENERGY',    $
                  'ENERGY','DENERGY','EFF','BINS','NBINS','THETA','DTHETA','PHI',     $
                  'DPHI','DOMEGA','GF','GEOM_FACTOR','DEAD','MASS','CHARGE','SC_POT', $
                  'MAGF','BKG','DATA','VELOCITY']
;; Define new ESA PAD structure tags
new_tags       = ['DEADTIME','BTH','BPH','ANGLES']
;; Define ESA PAD structure tags
tags           = [old_tags,new_tags]
;; => Define dummy return structure
dumb_str       = CREATE_STRUCT(tags,'THEMIS-',s,s,i,s,s,b,d,d,d,d,f_32x88,b,b,i,i,i,i,$
                               f_32x88,f_32x88,d_32x88,i_32x88,i,f_32x88,f_32x88,     $
                               f_32x88,f_32x88,f_32x88,f_32x88,f,f,f,f,f,f_3,f_32x88, $
                               f_32x88,d_3,f_32x88,f,f,f_32x88)
;;----------------------------------------------------------------------------------------
;; => Check input
;;----------------------------------------------------------------------------------------
IF (N_PARAMS() EQ 0) THEN BEGIN
  ;; no input => return default
  RETURN,dumb_str
ENDIF
str            = dat[0]   ; => in case it is an array of structures of the same format
tests          = (SIZE(str,/TYPE) NE 8L)
IF (tests) THEN BEGIN
  MESSAGE,notstr_mssg[0],/INFORMATIONAL,/CONTINUE
  RETURN,dumb_str
ENDIF

testf          = test_themis_esa_struc_format(str,/NOM) NE 1
IF (testf) THEN BEGIN
  MESSAGE,badstr_themis[0],/INFORMATIONAL,/CONTINUE
  RETURN,dumb_str
ENDIF
;;----------------------------------------------------------------------------------------
;; => Define structure information
;;----------------------------------------------------------------------------------------
;; => Define header information
proj_name      = dat[0].PROJECT_NAME         ;;  e.g. 'THEMIS-B'
sc_name        = dat[0].SPACECRAFT           ;;  e.g. 'b'
data_name      = dat[0].DATA_NAME+' PAD'     ;;  e.g. 'IESA 3D burst PAD'
apid_val       = dat[0].APID                 ;;  
unit_name      = dat[0].UNITS_NAME           ;;  e.g. 'counts'
unit_proc      = dat[0].UNITS_PROCEDURE      ;;  e.g. 'thm_convert_esa_units'
valid_val      = dat[0].VALID
time_val       = dat[0].TIME                 ;;  Unix time at start of distribution
etime_val      = dat[0].END_TIME             ;;  Unix time at end   of distribution
delt_val       = dat[0].DELTA_T              ;;  = (END_TIME - TIME)
intt_val       = dat[0].INTEG_T              ;;  = DELTA_T/1024
config1        = dat[0].CONFIG1
config2        = dat[0].CONFIG2
an_ind         = dat[0].AN_IND
en_ind         = dat[0].EN_IND
mode           = dat[0].MODE
;; => Define structure constants
geomfac        = dat[0].GEOM_FACTOR          ;;  total geometry factor [cm^(2) sr]
deadt          = dat[0].DEAD                 ;;  detector dead time [s]
mass           = dat[0].MASS                 ;;  particle mass [eV/c^2, with c in km/s]
charge         = dat[0].CHARGE               ;;  sign of particle charge
sc_pot         = dat[0].SC_POT               ;;  spacecraft potential [eV] estimate
magf           = dat[0].MAGF                 ;;  magnetic field vector [nT]
veloc          = dat[0].VELOCITY             ;;  bulk flow velocity estimate [km/s]
deadtime       = REPLICATE(deadt[0],n_e,n_a)
;;----------------------------------------------------------------------------------------
;; => Define return structure
;;----------------------------------------------------------------------------------------
scnames        = ['THEMIS-'+STRUPCASE(sc_name[0]),sc_name[0]]
dumb_str       = CREATE_STRUCT(tags,scnames[0],scnames[1],data_name[0],apid_val[0],     $
                               unit_name[0],unit_proc[0],valid_val[0],time_val[0],      $
                               etime_val[0],delt_val[0],intt_val[0],f_32x88,config1[0], $
                               config2[0],an_ind[0],en_ind[0],mode[0],FIX(n_e),f_32x88, $
                               f_32x88,d_32x88,i_32x88,FIX(n_a),f_32x88,f_32x88,f_32x88,$
                               f_32x88,f_32x88,f_32x88,geomfac[0],deadt[0],mass[0],     $
                               charge[0],sc_pot[0],magf,f_32x88,f_32x88,veloc,deadtime, $
                               f,f,f_32x88)

;;----------------------------------------------------------------------------------------
;; => Return structure to user
;;----------------------------------------------------------------------------------------

RETURN,dumb_str
END


;+
;*****************************************************************************************
;
;  FUNCTION :   themis_esa_pad.pro
;  PURPOSE  :   Creates a pitch-angle distribution (PAD) from a THEMIS ESA data
;                 structure that can be plotted using my_padplot_both.pro.
;
;  CALLED BY:   
;               my_padplot_both.pro
;
;  CALLS:
;               themis_esa_pad_template.pro
;               test_themis_esa_struc_format.pro
;               xyz_to_polar.pro
;               pangle.pro
;               interp.pro
;
;  REQUIRES:    
;               1)  THEMIS TDAS IDL libraries and UMN Modified Wind/3DP IDL Libraries
;
;  INPUT:
;               DAT    :  Scalar structure associated with a known THEMIS ESA data
;                           structure
;                           [see get_th?_pe%$.pro, ? = a-f, % = i,e, $ = b,f,r]
;
;  EXAMPLES:    
;               
;
;  KEYWORDS:    
;               MAGF   :  [3]-Element array defining the magnetic field vector to use to
;                           create the PAD
;                           [Default = DAT.MAGF]
;               ESTEPS :  [2]-Element array specifying the first and last energy bins
;                           to keep in the returned structure
;                           [Default = [0,DAT.NENERGY - 1L]]
;               BINS   :  [N]-Element array define which solid angle bins to sum over
;                           when calculating the PAD
;                           [Default = INDGEN(DAT.NBINS)]
;               NUM_PA :  Scalar defining the number of pitch-angle bins to create
;                           [Default = 8]
;               
;
;   CHANGED:  1)  NA [MM/DD/YYYY   v1.0.0]
;
;   NOTES:      
;               This routine is specific to THEMIS ESA data structures
;
;   CREATED:  08/15/2012
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  08/15/2012   v1.0.0
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

FUNCTION themis_esa_pad,dat,MAGF=magf,ESTEPS=esteps,BINS=bins,NUM_PA=num_pa

;;----------------------------------------------------------------------------------------
;; => Define some constants and dummy variables
;;----------------------------------------------------------------------------------------
f              = !VALUES.F_NAN  ;; dummy scalar [float]
d              = !VALUES.D_NAN  ;; dummy scalar [double]
;;----------------------------------------------------------------------------------------
;; => Check input
;;----------------------------------------------------------------------------------------
IF (N_PARAMS() EQ 0) THEN BEGIN
  ;; no input => return default
  RETURN,themis_esa_pad_template()
ENDIF
;;  Define temporary 

str            = dat[0]   ; => in case it is an array of structures of the same format
tests          = (SIZE(str,/TYPE) NE 8L)
testf          = test_themis_esa_struc_format(str,/NOM) NE 1
test           = tests OR testf
IF (test) THEN BEGIN
  ;; input must be an ESA structure
  RETURN,themis_esa_pad_template(str)
ENDIF
;;----------------------------------------------------------------------------------------
;; => Define data structure parameters
;;----------------------------------------------------------------------------------------
;; Define default # of energy and solid angle bins
n_e            = dat[0].NENERGY                   ; => default # of energy bins
n_a            = dat[0].NBINS                     ; => default # of solid angle bins

data0          = dat[0].DATA
geom0          = dat[0].GF                        ;; relative geometry factor per bin
dt0            = dat[0].DT_ARR                    ;; normalized anode accumulation times [unitless]
energy0        = dat[0].ENERGY                    ;; energy bin values [eV]
denergy0       = dat[0].DENERGY
deadtime0      = REPLICATE(dat[0].DEAD,n_e,n_a)   ;; Amptek A121 preamp deadtime [s]
count0         = REPLICATE(0,n_e,n_a)             ;; # of points calculated
eff0           = dat[0].EFF                       ;; efficiency and deadtime corrections
bkg0           = dat[0].BKG                       ;; estimate of background/noise
theta0         = dat[0].THETA
phi0           = dat[0].PHI
;; Calculate the average energy bin values [eV]
avg_ener       = TOTAL(energy0,2L,/NAN)/TOTAL(FINITE(energy0),2L,/NAN)
;;----------------------------------------------------------------------------------------
;; => Check keywords
;;----------------------------------------------------------------------------------------
enbins         = [0L,n_e - 1L]
;; Define # of pitch-angle bins on output
IF ~KEYWORD_SET(num_pa) THEN pang  = 8 ELSE pang  = (num_pa[0] < n_a[0]/3L) > 1L
;; Define # of energy bins on output
IF KEYWORD_SET(esteps) THEN BEGIN
  test = (N_ELEMENTS(esteps) NE 2L)
  IF (test) THEN nener = enbins ELSE nener = esteps[SORT(esteps)]
ENDIF ELSE BEGIN
  nener = enbins
ENDELSE
;; Define # of solid angle bins to sum over
IF (N_ELEMENTS(bins) NE 0) THEN ind = WHERE(bins) ELSE ind = INDGEN(dat[0].NBINS)

IF NOT KEYWORD_SET(magf) THEN BEGIN
  gmagf = WHERE(FINITE(dat[0].MAGF),gmag)
  IF (gmag GT 2) THEN BEGIN
    ;; tag is defined and finite
    magf = dat[0].MAGF
  ENDIF ELSE BEGIN
    ;; tag is not finite
    RETURN,themis_esa_pad_template(str)
  ENDELSE
ENDIF ELSE BEGIN
  ;; keyword set => check format
  testm = (N_ELEMENTS(magf) NE 3L)
  testd = (TOTAL(FINITE(dat[0].MAGF)) LE 2)
  IF (testm) THEN BEGIN
    ;; keyword not set => check structure tag values
    IF (testd) THEN BEGIN
      ;; tag values are not finite
      RETURN,themis_esa_pad_template(str,ESTEPS=nener,NUM_PA=pang[0])
    ENDIF ELSE BEGIN
      ;; tag values are good
      magf = dat[0].MAGF
    ENDELSE
  ENDIF ELSE BEGIN
    ;; keyword set => good format
    magf = magf
  ENDELSE
ENDELSE
;;----------------------------------------------------------------------------------------
;; => Define dummy data variables
;;----------------------------------------------------------------------------------------
e_ind          = LINDGEN(n_e)                  ;; indices for energies
nbins          = N_ELEMENTS(ind)
data           = FLTARR(n_e[0],pang[0])        ;; => Dummy array of PAD data
geom           = FLTARR(n_e[0],pang[0])        ;; => " " geometry factor data
dt             = FLTARR(n_e[0],pang[0])        ;; => " " Integration times (s)
energy         = FLTARR(n_e[0],pang[0])        ;; => " " energies (eV)
denergy        = FLTARR(n_e[0],pang[0])        ;; => " " differential energies (eV)
pangles        = FLTARR(n_e[0],pang[0])        ;; => " " pitch-angles (deg)
count          = FLTARR(n_e[0],pang[0])        ;; => " " # of points calculated
deadtime       = FLTARR(n_e[0],pang[0])        ;; => " " times when plate detector is off (s)
eff            = FLTARR(n_e[0],pang[0])        ;; => ESA efficiency
;;----------------------------------------------------------------------------------------
;; => Convert B-field vector to polar coordinates and calculate pitch-angles
;;----------------------------------------------------------------------------------------
xyz_to_polar,magf,THETA=bth,PHI=bph
; => Calculate Pitch-Angles from data and B-field
pa             = pangle(theta0,phi0,bth,bph)
pab            = FIX(pa/18e1*pang[0])  < (pang[0] - 1)
IF (ABS(bth) GT 9e1) THEN pab[*,*] = 0   ;; remove non-physical solutions
;;----------------------------------------------------------------------------------------
;; => Calculate Pitch-Angle Distributions (PADs)
;;----------------------------------------------------------------------------------------
FOR i=0L, nbins - 1L DO BEGIN
  b        = ind[i]            ;;  Solid angle index
;  e        = e_ind             ;;  Energy bin array elements
;   n_e      = e
  n_b      = pab[e_ind,b]      ;;  Bins to use for pitch-angle estimates
  ;;  Define the elements of PAB to sum over
  n_b_indx = WHERE(n_b GE 0 AND n_b LT pang[0],n_b_cnt)
  IF (n_b_cnt GT 0) THEN BEGIN
    e2                 = e_ind[n_b_indx]   ;; good energy bin elements
    ne2                = e2
    nb2                = n_b[n_b_indx]
    ;;------------------------------------------------------------------------------------
    ;; sum constituent elements
    ;;------------------------------------------------------------------------------------
    data[ne2,nb2]     += data0[e2,b]       ;; data
    geom[ne2,nb2]     += geom0[e2,b]       ;; geometry factors
    dt[ne2,nb2]       += dt0[e2,b]         ;; normalized accumulation times
    energy[ne2,nb2]   += energy0[e2,b]     ;; energies [eV]
    denergy[ne2,nb2]  += denergy0[e2,b]    ;; uncertainty in energies [eV]
    pangles[ne2,nb2]  += pa[e2,b]          ;; pitch-angles [deg]
    count[ne2,nb2]    += 1
    deadtime[ne2,nb2] += deadtime0[e2,b]   ;; deadtimes [s]
    eff[ne2,nb2]      += eff0[e2,b]        ;; efficiencies
  ENDIF
ENDFOR
;;  Normalize by the # of PAs calculated
energy        /= count
denergy       /= count
pangles       /= count
eff           /= count
IF (STRLOWCASE(dat[0].UNITS_NAME) NE 'counts') THEN data /= count
;;----------------------------------------------------------------------------------------
;; => Get rid of non-finite or negative energy bin values
;;----------------------------------------------------------------------------------------
bad            = WHERE(energy LE 0.0 OR FINITE(energy) EQ 0,bd,COMPLEMENT=good,NCOMPLEMENT=gd)
IF (bd GT 0) THEN BEGIN
  ;; get rid of "bad" energy bin values
  avge_2d     = avg_ener # REPLICATE(1e0,pang[0])
  energy[bad] = avge_2d[bad]
ENDIF
;;----------------------------------------------------------------------------------------
;; => Get rid of non-finite or negative PAs
;;----------------------------------------------------------------------------------------
avg_pangs      = TOTAL(pangles,1,/NAN)/TOTAL(FINITE(pangles),1,/NAN)
bad            = WHERE(pangles LE 0.0 OR FINITE(pangles) EQ 0,bd,COMPLEMENT=good,NCOMPLEMENT=gd)
IF (bd GT 0) THEN BEGIN
  ;; get rid of "bad" PAs data
  avgpa_2d     =  REPLICATE(1e0,n_e[0]) # avg_pangs
  pangles[bad] = avgpa_2d[bad]
ENDIF
;;----------------------------------------------------------------------------------------
;; => Interpolate across "bad" data points
;;----------------------------------------------------------------------------------------
bad            = WHERE(data LE 0.0 OR FINITE(data) EQ 0,bd,COMPLEMENT=good,NCOMPLEMENT=gd)
IF (bd GT 0) THEN BEGIN
  ;; get rid of "bad" data
  FOR j=0L, n_e[0] - 1L DO BEGIN
    t_data = REFORM(data[j,*])
    t_pang = REFORM(pangles[j,*])
    bad    = WHERE(t_data LE 0.0 OR FINITE(t_data) EQ 0,bd,COMPLEMENT=good,NCOMPLEMENT=gd)
    IF (gd GT 2) THEN BEGIN
      ;; > 2 finite points => linearly interpolate w/o extrapolation
      temp   = interp(t_data[good],t_pang[good],t_pang,/NO_EXTRAP)
    ENDIF ELSE BEGIN
      temp   = t_data
    ENDELSE
    data[j,*] = temp
  ENDFOR
ENDIF
;;----------------------------------------------------------------------------------------
;; => Define desired energy bins
;;----------------------------------------------------------------------------------------
;; constrain energy bin definition
nener[0]       = (nener[0] > 0) < (n_e[0] - 1L)
nener[1]       = (nener[1] > 0) < (n_e[0] - 1L)
;; Redefine default # of energy and solid angle bins
n_e            = nener[1] - nener[0] + 1L  ;; # of energy bins on output
;; => Find energy bin indices
mine           = MIN(avg_ener[nener],/NAN,len)
maxe           = MAX(avg_ener[nener],/NAN,lex)
;;  ESA energy bin values go from HIGH -> LOW
s_e            = nener[lex[0]]
e_e            = nener[len[0]]
e_ind          = LINDGEN(n_e) + s_e[0]     ;; indices for energies
;;----------------------------------------------------------------------------------------
;; => Get dummy structure to fill
;;----------------------------------------------------------------------------------------
pad_str        = themis_esa_pad_template(dat[0],ESTEPS=nener,NUM_PA=pang[0])
;;----------------------------------------------------------------------------------------
;; => Keep only desired energy bins
;;----------------------------------------------------------------------------------------
newd           = data[e_ind,*]
geom           = geom[e_ind,*]
dt             = dt[e_ind,*]
energy         = energy[e_ind,*]
denergy        = denergy[e_ind,*]
newp           = pangles[e_ind,*]
count          = count[e_ind,*]
deadtime       = deadtime[e_ind,*]
eff            = eff[e_ind,*]
;;----------------------------------------------------------------------------------------
;; => Fill dummy structure
;;----------------------------------------------------------------------------------------
str_element,pad_str,'DATA',newd,/ADD_REPLACE
str_element,pad_str,'GF',geom,/ADD_REPLACE
str_element,pad_str,'DT_ARR',dt,/ADD_REPLACE
str_element,pad_str,'ENERGY',energy,/ADD_REPLACE
str_element,pad_str,'DENERGY',denergy,/ADD_REPLACE
str_element,pad_str,'ANGLES',newp,/ADD_REPLACE
str_element,pad_str,'DEADTIME',deadtime,/ADD_REPLACE
str_element,pad_str,'EFF',eff,/ADD_REPLACE
str_element,pad_str,'BTH',bth,/ADD_REPLACE
str_element,pad_str,'BPH',bph,/ADD_REPLACE
;;----------------------------------------------------------------------------------------
;; => Return structure to user
;;----------------------------------------------------------------------------------------

RETURN,pad_str
END
