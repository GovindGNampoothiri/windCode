;+
;*****************************************************************************************
;
;  FUNCTION :   modify_themis_esa_struc.pro
;  PURPOSE  :   This routine takes an ESA structure produced by the THEMIS routines and
;                 modifies it so that it is compatible with UMN Modified Wind/3DP IDL
;                 Library plotting routines.
;
;  CALLED BY:   
;               NA
;
;  CALLS:
;               test_themis_esa_struc_format.pro
;               str_element.pro
;
;  REQUIRES:    
;               1)  UMN Modified Wind/3DP IDL Libraries
;
;  INPUT:
;               DAT      :  Scalar THEMIS ESA data structure
;
;  EXAMPLES:    
;               NA
;
;  KEYWORDS:    
;               NA
;
;   CHANGED:  1)  NA [MM/DD/YYYY   v1.0.0]
;
;   NOTES:      
;               1)  This routine modifies the input, so make sure you have a copy
;               2)  Some of the structure tags are added so that the structure will
;                     cause test_3dp_struc_format.pro to return TRUE
;
;   CREATED:  03/14/2012
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  03/14/2012   v1.0.0
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

PRO modify_themis_esa_struc,dat

;-----------------------------------------------------------------------------------------
; => Define some constants and dummy variables
;-----------------------------------------------------------------------------------------
f              = !VALUES.F_NAN
d              = !VALUES.D_NAN
notstr_mssg    = 'Must be an IDL structure...'
;-----------------------------------------------------------------------------------------
; => Check input
;-----------------------------------------------------------------------------------------
IF N_PARAMS() EQ 0 THEN RETURN
str  = dat[0]   ; => in case it is an array of structures of the same format
IF (SIZE(str,/TYPE) NE 8L) THEN BEGIN
  MESSAGE,notstr_mssg,/INFORMATIONAL,/CONTINUE
  RETURN
ENDIF
test = test_themis_esa_struc_format(str) NE 1
IF (SIZE(str,/TYPE) NE 8L) THEN RETURN

dat  = dat[0]
;-----------------------------------------------------------------------------------------
; => Modify input
;-----------------------------------------------------------------------------------------
; => use my modified unit conversion routine so we don't need TDAS libraries
str_element,dat,'UNITS_PROCEDURE','thm_convert_esa_units_lbwiii',/ADD_REPLACE
; => add the Vsw structure tag
str_element,dat,'VSW',dat.VELOCITY,/ADD_REPLACE
; => Change project name to include spacecraft
nprojnm  = dat.PROJECT_NAME[0]+'-'+STRUPCASE(dat.SPACECRAFT[0])
str_element,dat,'PROJECT_NAME',nprojnm[0],/ADD_REPLACE
; => copy accumulation times array with new tag
str_element,dat,'DT',dat.DT_ARR,/ADD_REPLACE
; => create an array of dead times
deadtime = REPLICATE(dat[0].DEAD[0],dat[0].NENERGY,dat[0].NBINS)
str_element,dat,'DEADTIME',deadtime,/ADD_REPLACE

;str_element,dat,'',dat.,/ADD_REPLACE
;-----------------------------------------------------------------------------------------
; => Return
;-----------------------------------------------------------------------------------------

RETURN
END