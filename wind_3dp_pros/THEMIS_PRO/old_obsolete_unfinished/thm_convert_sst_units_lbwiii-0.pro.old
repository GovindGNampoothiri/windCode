;+
;*****************************************************************************************
;
;  PROCEDURE:   thm_convert_sst_units_lbwiii.pro
;  PURPOSE  :   Converts the data of THEMIS SST data structure to the user defined
;                 units.  The input data structure tags DATA, DDATA, and UNITS_NAME are
;                 altered by this routine.
;
;  CALLED BY:   
;               conv_units.pro
;
;  INCLUDES:
;               NA
;
;  CALLS:
;               test_themis_esa_struc_format.pro
;               in_set.pro
;               thm_sst_atten_scale.pro
;               thm_part_decomp16.pro
;               dprint.pro
;               time_string.pro
;               struct_value.pro
;
;  REQUIRES:    
;               1)  UMN Modified Wind/3DP and SPEDAS IDL Libraries
;
;  INPUT:
;               DATA               :  [N]-element array of THEMIS SST data structures
;               UNITS              :  Scalar [string] defining to which the units to
;                                       convert.  The following inputs are allowed:
;                                         'compressed'  ;  # of counts
;                                         'counts'      ;  # of counts
;                                         'rate'        ;  [s^(-1)]
;                                         'crate'       ;  [s^(-1)] scaled rate
;                                         'eflux'       ;  energy flux
;                                         'flux'        ;  number flux
;                                         'df'          ;  phase space density
;
;  EXAMPLES:    
;               thm_convert_sst_units_lbwiii,data,units,SCALE=scale
;
;  KEYWORDS:    
;               SCALE              :  Set to a named variable to return the conversion
;                                       factor array used to scale the data
;
;   CHANGED:  1)  Pat Cruce changed something...
;                                                                  [09/06/2013   v1.?.?]
;             2)  Re-wrote, cleaned up, and vectorized allowing for arrays of input
;                   data structures
;                                                                  [11/16/2015   v2.0.0]
;
;   NOTES:      
;               1)  Original version can be found in the latest SPEDAS release,
;                     called:  thm_sst_convert_units2.pro
;                     found at:
;                     http://themis.ssl.berkeley.edu/socware/bleeding_edge/
;               2)  Fractional counts are forced to be used as default
;
;  REFERENCES:  
;               1)  Carlson et al., (1983), "An instrument for rapidly measuring
;                      plasma distribution functions with high resolution,"
;                      Adv. Space Res. Vol. 2, pp. 67-70.
;               2)  Curtis et al., (1989), "On-board data analysis techniques for
;                      space plasma particle instruments," Rev. Sci. Inst. Vol. 60,
;                      pp. 372.
;               3)  Lin et al., (1995), "A Three-Dimensional Plasma and Energetic
;                      particle investigation for the Wind spacecraft," Space Sci. Rev.
;                      Vol. 71, pp. 125.
;               4)  Paschmann, G. and P.W. Daly (1998), "Analysis Methods for Multi-
;                      Spacecraft Data," ISSI Scientific Report, Noordwijk, 
;                      The Netherlands., Int. Space Sci. Inst.
;               5)  McFadden, J.P., C.W. Carlson, D. Larson, M. Ludlam, R. Abiad,
;                      B. Elliot, P. Turin, M. Marckwordt, and V. Angelopoulos
;                      "The THEMIS ESA Plasma Instrument and In-flight Calibration,"
;                      Space Sci. Rev. 141, pp. 277-302, (2008).
;               6)  McFadden, J.P., C.W. Carlson, D. Larson, J.W. Bonnell,
;                      F.S. Mozer, V. Angelopoulos, K.-H. Glassmeier, U. Auster
;                      "THEMIS ESA First Science Results and Performance Issues,"
;                      Space Sci. Rev. 141, pp. 477-508, (2008).
;               7)  Auster, H.U., K.-H. Glassmeier, W. Magnes, O. Aydogar, W. Baumjohann,
;                      D. Constantinescu, D. Fischer, K.H. Fornacon, E. Georgescu,
;                      P. Harvey, O. Hillenmaier, R. Kroth, M. Ludlam, Y. Narita,
;                      R. Nakamura, K. Okrafka, F. Plaschke, I. Richter, H. Schwarzl,
;                      B. Stoll, A. Valavanoglou, and M. Wiedemann "The THEMIS Fluxgate
;                      Magnetometer," Space Sci. Rev. 141, pp. 235-264, (2008).
;               8)  Angelopoulos, V. "The THEMIS Mission," Space Sci. Rev. 141,
;                      pp. 5-34, (2008).
;
;   ADAPTED FROM:  thm_sst_convert_units2.pro
;   CREATED:  ??/??/????
;   CREATED BY:  Pat Cruce
;    LAST MODIFIED:  11/16/2015   v2.0.0
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

PRO thm_convert_sst_units_lbwiii,data,units,SCALE=scale

;;----------------------------------------------------------------------------------------
;;  Define some constants and dummy variables
;;----------------------------------------------------------------------------------------
f              = !VALUES.F_NAN
d              = !VALUES.D_NAN
notstr_mssg    = 'Must be an IDL structure...'
badstr_themis  = 'Not an appropriate THEMIS ESA structure...'
notstring_msg  = 'UNITS must be a scalar string...'

cc3d           = FINDGEN(256)
;;  so scale gets passed back even if units = data.units_name
scale          = 1d0
;;----------------------------------------------------------------------------------------
;;  Check input
;;----------------------------------------------------------------------------------------
IF (N_PARAMS() LT 2) THEN RETURN
str            = data[0]   ; => in case it is an array of structures of the same format
IF (SIZE(str,/TYPE) NE 8) THEN BEGIN
  MESSAGE,notstr_mssg[0],/INFORMATIONAL,/CONTINUE
  RETURN
ENDIF
;;  check to make sure input is a THEMIS ESA structure
test           = test_themis_esa_struc_format(str) NE 1
IF (test[0]) THEN RETURN
;;  check to make sure input is a string
IF (SIZE(units,/TYPE) NE 7) THEN BEGIN
  MESSAGE,notstring_msg[0],/INFORMATIONAL,/CONTINUE
  RETURN
ENDIF
;;  check to make sure user is actually changing units
old_unit       = data[0].UNITS_NAME
new_unit       = units[0]
IF (STRUPCASE(new_unit[0]) EQ STRUPCASE(old_unit[0])) THEN RETURN
;;----------------------------------------------------------------------------------------
;;  These are vectorized for array of data structures
;;----------------------------------------------------------------------------------------
n_e            = data[0].NENERGY                 ;;  Number of energy bins
nbins          = data[0].NBINS                   ;;  Number of angle bins
n_str          = N_ELEMENTS(data)                ;;  Number of data structures
tags           = STRLOWCASE(TAG_NAMES(data[0]))  ;;  Structure tags names
;;  Define efficiencies
IF in_set(tags,'eff') THEN BEGIN
  eff = data.EFF
ENDIF ELSE BEGIN
  eff = 1e0
ENDELSE
energy         = data.ENERGY                     ;;  [n_e,nbins,n_str]-Element array of energies (eV)
denergy        = data.DENERGY                    ;;  [n_e,nbins,n_str]-Element array of energy ranges (eV)
;;  Define the total geometry factor of the detector [cm^2 sr]
gf_facs        = FLTARR(n_e,nbins,n_str)
mass           = FLTARR(n_e,nbins,n_str)         ;;  Particle mass [eV/c^2, with c in km/s]
atten          = INTARR(n_e,nbins,n_str)         ;;  4-bit attenuator flags
IF in_set(tags,'att') THEN att = data.ATT ELSE att = REPLICATE(1e0,n_e,nbins,n_str)
dead           = data.DEADTIME                   ;;  Define A121 preamp dead time [s]
dt             = data.INTEG_T                    ;;  Total accumulation times [s]
;;  Define attenuator values, geometry factors, mass, etc.
dims           = SIZE(REFORM(data[0].DATA),/DIMENSIONS)
FOR j=0L, n_str - 1L DO BEGIN
  ;;  Total geometry factor of the detector [cm^2 sr]
  gf_facs[*,*,j] = data[j].GEOM_FACTOR[0]
  ;;  Particle mass [eV/c^2, with c in km/s]
  mass[*,*,j]    = data[j].MASS[0]
  ;;  Attenuator values
  t_att           = REFORM(att[*,*,j])
  atten[*,*,j]    = thm_sst_atten_scale(data[j].ATTEN,dims,SCALE_FACTORS=t_att)
ENDFOR
;IF in_set(tags,'att') THEN BEGIN
;  atten = thm_sst_atten_scale(data.ATTEN,dimen(data.DATA),SCALE_FACTORS=data.ATT)
;endif else begin
;  atten = thm_sst_atten_scale(data.ATTEN,dimen(data.DATA))
;endelse

;;  Define modified geometry factors
gf             = gf_facs*data.GF*eff*atten*denergy*eff
;;----------------------------------------------------------------------------------------
;;  Define scale factors to new units
;;----------------------------------------------------------------------------------------
scale          = 1.
CASE STRUPCASE(old_unit[0]) OF
  'COMPRESSED'  :  sfact = 1.d
  'COUNTS'      :  sfact = 1.
  'RATE'        :  sfact = 1. * dt                              ;;  # s^(-1)
  'EFLUX'       :  sfact = 1. * dt * gf / energy                ;;  eV cm^(-2) s^(-1) sr^(-1) eV^(-1)
  'FLUX'        :  sfact = 1. * dt * gf                         ;;  # cm^(-2) s^(-1) sr^(-1) eV^(-1)
  'DF'          :  sfact = 1. * dt * gf * energy * 2e5/mass^2   ;;  # cm^(-3) km^(3) s^(-3)
  ELSE          : BEGIN
    MESSAGE,'Unknown starting units: ',old_unit[0]
    RETURN
  END
ENDCASE
scale         *= sfact
;;----------------------------------------------------------------------------------------
;;  Convert back to counts
;;----------------------------------------------------------------------------------------
tmp            = data.DATA
IF (STRUPCASE(old_unit[0]) EQ 'COMPRESSED') THEN tmp = thm_part_decomp16(BYTE(tmp))
tmp           *= scale
;;----------------------------------------------------------------------------------------
;;  Remove dead time corrections
;;----------------------------------------------------------------------------------------
test           = (STRUPCASE(old_unit[0]) NE 'COUNTS') AND (STRUPCASE(old_unit[0]) NE 'RATE') $
                  AND (STRUPCASE(old_unit[0]) NE 'COMPRESSED')
IF (test[0]) THEN BEGIN
  temp  = tmp / (1e0 + (dead/dt)*tmp)
  tmp  *= temp
;  tmp = tmp / (1+(dead/dt)*tmp)
ENDIF
;;----------------------------------------------------------------------------------------
;;  Define scale factors for new units
;;----------------------------------------------------------------------------------------
CASE STRUPCASE(new_unit[0]) OF 
  'COUNTS'      :  sfact = 1.
  'RATE'        :  sfact = 1. / (dt)
  'EFLUX'       :  sfact = 1. / (dt * gf * energy)
  'FLUX'        :  sfact = 1. / (dt * gf)
  'DF'          :  sfact = 1. / (dt * gf * energy * 2e5/mass^2)
  ELSE: BEGIN
    MESSAGE,'Undefined units: ',new_unit[0]
    RETURN
  END
ENDCASE
scale1         = sfact
;;----------------------------------------------------------------------------------------
;;  Dead time correct data if not counts or rate
;;----------------------------------------------------------------------------------------
test           = (STRUPCASE(new_unit[0]) NE 'COUNTS') AND (STRUPCASE(new_unit[0]) NE 'RATE')
IF (test[0]) THEN BEGIN
  ;;--------------------------------------------------------------------------------------
  ;;  correct for dead time
  ;;--------------------------------------------------------------------------------------
  temp  = FLTARR(n_e,nbins)
  denom = 1. - (dead*tmp/dt)
  tmp2  = FLTARR(n_e,nbins,n_str)
  FOR j=0L, n_str - 1L DO BEGIN
    temp  = REFORM(denom[*,*,j])
    void  = WHERE(temp LT .2,count)
    IF (count GT 0) THEN BEGIN
      dprint,DLEVEL=1,MIN(denom,ind)
      dprint,DLEVEL=1,' Error: sst_convert_units dead time error.'
      dprint,DLEVEL=1,' Dead time correction limited to x5 for ',count,' bins'
      dprint,DLEVEL=1,' Time= ',time_string(data[j].TIME[0],/MSEC)
      temp[void] = !VALUES.F_NAN
;      denom[void] = !VALUES.F_NAN
    ENDIF
    temp         = (0.2 > temp) < 1e0
    ;;  Fill denom
    denom[*,*,j] = temp
  ENDFOR
  ;;  Define corrected values
  tmp2 = tmp/denom
ENDIF ELSE BEGIN
  ;;--------------------------------------------------------------------------------------
  ;;  Do not correct for dead time
  ;;--------------------------------------------------------------------------------------
  tmp2 = tmp
ENDELSE
;;----------------------------------------------------------------------------------------
;;  Scale to new units
;;----------------------------------------------------------------------------------------
data.UNITS_NAME = units[0]
ddata0         = struct_value(data[0],'ddata',INDEX=index)
IF (index[0] GE 0) THEN data.DDATA = scale1 * tmp2^(1./2.)
;;----------------------------------------------------------------------------------------
;;  Redefine data tag
;;----------------------------------------------------------------------------------------
temp           = scale1 * tmp2
;data.DATA      = scale1 * tmp2
scale         *= scale1
;;  Check output [set NaNs to zeros]
idx            = WHERE(~FINITE(temp),c)
;idx            = WHERE(~FINITE(data.DATA),c)
IF (c GT 0) THEN BEGIN
  temp[idx] = 0
;  data.DATA[idx] = 0
ENDIF
data.DATA      = temp
;;----------------------------------------------------------------------------------------
;;  Return to user
;;----------------------------------------------------------------------------------------

RETURN
END





