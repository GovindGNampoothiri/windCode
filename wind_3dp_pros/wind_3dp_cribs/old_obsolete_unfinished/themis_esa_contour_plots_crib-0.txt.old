;-----------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------
; => From THEMIS prompt
;-----------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------

;-----------------------------------------------------------------------------------------
; => Constants
;-----------------------------------------------------------------------------------------
f      = !VALUES.F_NAN
d      = !VALUES.D_NAN
epo    = 8.854187817d-12   ; => Permittivity of free space (F/m)
muo    = 4d0*!DPI*1d-7     ; => Permeability of free space (N/A^2 or H/m)
me     = 9.10938291d-31    ; => Electron mass (kg) [2010 value]
mp     = 1.672621777d-27   ; => Proton mass (kg) [2010 value]
ma     = 6.64465675d-27    ; => Alpha-Particle mass (kg) [2010 value]
qq     = 1.602176565d-19   ; => Fundamental charge (C) [2010 value]
kB     = 1.3806488d-23     ; => Boltzmann Constant (J/K) [2010 value]
K_eV   = 1.1604519d4       ; => Factor [Kelvin/eV] [2010 value]
c      = 2.99792458d8      ; => Speed of light in vacuum (m/s)
R_E    = 6.37814d3         ; => Earth's Equitorial Radius (km)

;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
; => Change these paths accordingly (this is what is found in comp_lynn_pros.pro)
;     1)  These routines depend upon the user putting the /wind_3dp_pros directory in
;           their working IDL directory
;     1)  Compile these at least twice
;+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/kill_data_tr.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/kill_data_tr.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/array_where.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/array_where.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/sample_rate.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/sample_rate.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/TPLOT_PRO/time_bar.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/TPLOT_PRO/time_bar.pro

.compile /Users/lbwilson/Desktop/idllibs/codemgr/libs/utility/difference.pro
.compile /Users/lbwilson/Desktop/idllibs/codemgr/libs/utility/findpath.pro
.compile /Users/lbwilson/Desktop/idllibs/codemgr/libs/utility/findsource.pro
.compile /Users/lbwilson/Desktop/idllibs/codemgr/libs/utility/read_wholefile.pro
.compile /Users/lbwilson/Desktop/idllibs/codemgr/libs/utility/man.pro

.compile /Users/lbwilson/Desktop/swidl-0.1/IDL_stuff/themis_pros/wave_polarization.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/IDL_stuff/themis_pros/wavpol_to_tplot.pro


.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/my_crossp_2.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/my_dot_prod.pro

.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/rh_pros/vshn_calc.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/rh_pros/vec_norm.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/rh_pros/vec_trans.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/rh_pros/vec_cross.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/rh_pros/rh_resize.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/rh_pros/del_vsn.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/rh_pros/rh_eq_gen.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/rh_pros/rh_eq_chisq.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/rh_pros/rh_solve_lmq.pro


.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/THEMIS_PRO/dat_themis_esa_str_names.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/dat_3dp_str_names.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/test_3dp_struc_format.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/THEMIS_PRO/test_themis_esa_struc_format.pro

.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/THEMIS_PRO/test_wind_vs_themis_esa_struct.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/THEMIS_PRO/modify_themis_esa_struc.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/THEMIS_PRO/thm_convert_esa_units_lbwiii.pro

.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/my_str_date.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/my_time_string.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/file_name_times.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/time_range_define.pro

.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/dat_3dp_energy_bins.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/contour_3d_1plane.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/read_gen_ascii.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/pesa_high_bad_bins.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/transform_vframe_3d.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/rotate_3dp_structure.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/SCIENCE_PRO/convert_ph_units.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/add_scpot.pro
; => Put the initialization routine (comp_lynn_pros.pro) in the ~/TDAS/tdas_6_00/idl/
;      directory and change the file paths so they work for your personal machine

; => Compile necessary routines
@comp_lynn_pros
; => initialize themis routines and default parameters
thm_init

;-----------------------------------------------------------------------------------------
; => Date/Times/Probes
;-----------------------------------------------------------------------------------------
tdate     = '2009-07-13'
probe     = 'b'
probef    = probe[0]
gprobes   = probe[0]
tr_00     = tdate[0]+'/'+['07:50:00','10:10:00']
; Data parameters, load data, and set color table:
;
dur       = 1.0   ; # of days

timespan,tdate[0],dur[0],/DAY
tr        = timerange()
probes    = ['a','b','c','d','e']

tplot_options,'XMARGIN',[ 20, 15]
tplot_options,'YMARGIN',[ 5, 5]
;;=================================================================
;; Load state stuff
;;=================================================================
sc        = probe[0]
thm_load_state,PROBE=sc[0],/GET_SUPPORT_DATA
;;=================================================================
;; Load magnetic field and level 2 velocity data (for coord rotation).
;;=================================================================
mode   = 'fg?'
sc     = probe[0]
thm_load_fgm,PROBE=sc[0],DATATYPE=mode[0],LEVEL=2,COORD='all',TRANGE=tr

WINDOW,0,RETAIN=2,XSIZE=1700,YSIZE=1100
sc     = probe[0]
pref   = 'th'+sc[0]+'_fgl'+['_dsl','_gse','_gsm']
names  = tnames(pref)
tplot,names
;-----------------------------------------------------------------------------------------
; => Convert GSM SC position to SM position
;-----------------------------------------------------------------------------------------
sc      = probe[0]
pos_gsm = 'th'+sc[0]+'_state_pos_gsm'
pos__sm = 'th'+sc[0]+'_state_pos__sm'
cotrans,pos_gsm[0],pos__sm[0],/GSM2SM
posnm          = pos__sm[0]
get_data,posnm[0],DATA=temp,DLIM=dlim,LIM=lim
th_pos_time    = temp.X
th_pos_vec__sm = temp.Y
th_pos_rad__sm = SQRT(TOTAL(th_pos_vec__sm^2,2,/NAN))
;-----------------------------------------------------------------------------------------
; => Calculate Magnetic Local Time (MLT), Magnetic Latitude (MLAT), L-Shell, and 
;      Invariant Latitude (ILAT)
;-----------------------------------------------------------------------------------------
;-----------------------------------------------------
; => MLT (hours)
;-----------------------------------------------------
t_x            = th_pos_vec__sm[*,0]
t_y            = th_pos_vec__sm[*,1]
t_z            = th_pos_vec__sm[*,2]
th_mlt         = ATAN(t_y/t_x)*18d1/!DPI/15d0 + 12d0
; => Check for negative X-SM coordinate points
low_tmp        = WHERE(th_pos_vec__sm[*,0] LT 0d0,lwtp)
IF (lwtp GT 0L) THEN th_mlt[low_tmp] = (ATAN(t_y[low_tmp]/t_x[low_tmp]) + !DPI)*18d1/(!DPI*15d0) + 12d0
; => make sure LT 24
th_mlt         = th_mlt MOD 24d0
;-----------------------------------------------------
; => MLAT (deg)
;-----------------------------------------------------
t_ratio        = t_z/th_pos_rad__sm
th_mlat        = ATAN(t_ratio)*18d1/!DPI
;-----------------------------------------------------
; => L-Shell (Re)
;-----------------------------------------------------
cmlat          = COS(th_mlat*!DPI/18d1)
th_lshell      = th_pos_rad__sm/(R_E[0]*cmlat^2)
;-----------------------------------------------------
; => ILAT (deg)
;-----------------------------------------------------
irt_lsh        = SQRT(1d0/th_lshell)
th_ilat        = ACOS(irt_lsh)*18d1/!DPI

; => send to TPLOT
pref           = 'th'+sc[0]+'_'
store_data,pref[0]+'_MLT',DATA={X:th_pos_time,Y:th_mlt}
store_data,pref[0]+'MLAT',DATA={X:th_pos_time,Y:th_mlat}
store_data,pref[0]+'_LSH',DATA={X:th_pos_time,Y:th_lshell}
store_data,pref[0]+'ILAT',DATA={X:th_pos_time,Y:th_ilat}

pref           = 'th'+sc[0]+'_'
tpref          = 'th'+sc[0]+' '
options,pref[0]+'_MLT','YTITLE',tpref[0]+'MLT [Hr]',/DEF
options,pref[0]+'MLAT','YTITLE',tpref[0]+'MLAT [Deg]',/DEF
options,pref[0]+'_LSH','YTITLE',tpref[0]+'LShell [Re]',/DEF
options,pref[0]+'ILAT','YTITLE',tpref[0]+'ILAT [Deg]',/DEF

nnw = tnames()
options,nnw,'YSTYLE',1
options,nnw,'PANEL_SIZE',2.
options,nnw,'XMINOR',5
options,nnw,'XTICKLEN',0.04
options,nnw,'YTICKLEN',0.01

; => Add SC position to TPLOT plots
names          = [pref[0]+'_LSH',pref[0]+'_MLT',pref[0]+'MLAT',pref[0]+'ILAT']
tplot_options,VAR_LABEL=names
;-----------------------------------------------------
; => Create |B| TPLOT variable
;-----------------------------------------------------
sc      = probe[0]
pref    = 'th'+sc[0]+'_fg*'
names   = tnames(pref[0])
hed_nm  = tnames('*_hed')
good_nm = array_where(names,hed_nm,/N_UNIQ,NCOMP1=comp1,NCOMP2=comp2)
names   = names[comp1]
options,names,'COLORS',[250,150, 50],/DEF
options,names,'LABELS',['x','y','z'],/DEF

mode    = 'fgl'
coord   = 'dsl'
name    = 'th'+sc[0]+'_'+mode[0]+'_'+coord[0]
get_data,name[0],DATA=temp,DLIM=dlim,LIM=lim
bmag    = SQRT(TOTAL(temp.Y^2,2,/NAN))
temp2   = {X:temp.X,Y:bmag}
store_data,'th'+sc[0]+'_'+mode[0]+'_mag',DATA=temp2
options,'th'+sc[0]+'_'+mode[0]+'_mag','YTITLE','th'+STRMID(sc[0],0L,1L)+' |B| ['+mode[0]+', nT]',/DEF

mode    = 'fgh'
coord   = 'dsl'
name    = 'th'+sc[0]+'_'+mode[0]+'_'+coord[0]
get_data,name[0],DATA=temp,DLIM=dlim,LIM=lim
bmag    = SQRT(TOTAL(temp.Y^2,2,/NAN))
temp2   = {X:temp.X,Y:bmag}
store_data,'th'+sc[0]+'_'+mode[0]+'_mag',DATA=temp2
options,'th'+sc[0]+'_'+mode[0]+'_mag','YTITLE','th'+STRMID(sc[0],0L,1L)+' |B| ['+mode[0]+', nT]',/DEF

nnw = tnames()
options,nnw,'YSTYLE',1
options,nnw,'PANEL_SIZE',2.
options,nnw,'XMINOR',5
options,nnw,'XTICKLEN',0.04
options,nnw,'YTICKLEN',0.01

mode    = 'fgl'
fgm_nm  = 'th'+STRMID(sc[0],0L,1L)+'_'+mode[0]+'_gsm'
options,fgm_nm[0],'YTITLE','th'+STRMID(sc[0],0L,1L)+' B ['+mode[0]+', GSM, nT]',/DEF
options,fgm_nm[0],'YSUBTITLE',/DEF

mode    = 'fgh'
fgm_nm  = 'th'+STRMID(sc[0],0L,1L)+'_'+mode[0]+'_gsm'
options,fgm_nm[0],'YTITLE','th'+STRMID(sc[0],0L,1L)+' B ['+mode[0]+', GSM, nT]',/DEF
options,fgm_nm[0],'YSUBTITLE',/DEF
;-----------------------------------------------------
; => Fix the Y-Axis Titles
;-----------------------------------------------------
mode    = 'fg'+['s','l','h']
coord   = ['dsl','gse','gsm','mag']
modeu   = STRUPCASE(mode)
coordu  = STRUPCASE(coord)

FOR j=0L, 2L DO BEGIN                                                               $
  FOR k=0L, 3L DO BEGIN                                                             $
    pref    = 'th'+sc[0]+'_'+mode[j]+'_'+coord[k]                                 & $
    names   = tnames(pref[0])                                                     & $
    get_data,names[0],DATA=temp,DLIM=dlim,LIM=lim                                 & $
    smrate  = sample_rate(temp.X,GAP_THRESH=2d0,/AVE)                             & $
    strsmr  = STRTRIM(STRING(FORMAT='(f15.0)',smrate[0]),2L)                      & $
    yttl    = 'B ['+modeu[j]+', '+coordu[k]+', nT]'                               & $
    ysubt   = '[th'+sc[0]+' '+strsmr[0]+' sps, L2]'                               & $
    str_element,dlim,'YTITLE',yttl[0],/ADD_REPLACE                                & $
    str_element,dlim,'YSUBTITLE',ysubt[0],/ADD_REPLACE                            & $
    store_data,names[0],DATA=temp,DLIM=dlim,LIM=lim


mode    = 'fgl'
names   = 'th'+sc[0]+'_'+mode[0]+['_mag','_gsm']
tplot,names,TRANGE=time_double(tr_00),/NOM
;-----------------------------------------------------------------------------------------
; => Load ESA data
;-----------------------------------------------------------------------------------------
; => Load level 2 densities and SC potentials
sc      = probe[0]
thm_load_esa,PROBE=sc[0],DATAT=' peer_avgtemp peir_avgtemp pe?r_density peer_sc_pot ',LEVEL=2
thm_load_esa,PROBE=sc[0],DATAT=' pee?_density pee?_avgtemp ',LEVEL=2
thm_load_esa,PROBE=sc[0],DATAT=' pei?_density pei?_avgtemp ',LEVEL=2
thm_load_esa,PROBE=sc[0],DATAT=' pee?_velocity_dsl pei?_velocity_dsl ',LEVEL=2
; => rotate DSL to GSE
partn     = ['e','i']
coords    = ['dsl','gse']
in_suffe  = 'pe'+partn[0]+['b','r','f']+'_velocity_'+coords[0]
out_suffe = 'pe'+partn[0]+['b','r','f']+'_velocity_'+coords[1]
in_suffi  = 'pe'+partn[1]+['b','r','f']+'_velocity_'+coords[0]
out_suffi = 'pe'+partn[1]+['b','r','f']+'_velocity_'+coords[1]
in_name   = 'th'+sc[0]+'_'+[[in_suffe],[in_suffi]]
out_name  = 'th'+sc[0]+'_'+[[out_suffe],[out_suffi]]
FOR j=0L, 1L DO BEGIN                                                              $
  thm_cotrans,in_name[0,j],out_name[0,j],OUT_COORD=coords[1],IN_COORD=coords[0]  & $
  thm_cotrans,in_name[1,j],out_name[1,j],OUT_COORD=coords[1],IN_COORD=coords[0]  & $
  thm_cotrans,in_name[2,j],out_name[2,j],OUT_COORD=coords[1],IN_COORD=coords[0]

names     = tnames([REFORM(in_name,3*2),REFORM(out_name,3*2)])
options,names,'COLORS',[250,150, 50],/DEF
options,names,'LABELS',['x','y','z'],/DEF

; => rotate GSE to GSM
partn     = ['e','i']
coords    = ['gse','gsm']
in_suffe  = 'pe'+partn[0]+['b','r','f']+'_velocity_'+coords[0]
out_suffe = 'pe'+partn[0]+['b','r','f']+'_velocity_'+coords[1]
in_suffi  = 'pe'+partn[1]+['b','r','f']+'_velocity_'+coords[0]
out_suffi = 'pe'+partn[1]+['b','r','f']+'_velocity_'+coords[1]
in_name   = 'th'+sc[0]+'_'+[[in_suffe],[in_suffi]]
out_name  = 'th'+sc[0]+'_'+[[out_suffe],[out_suffi]]
FOR j=0L, 1L DO BEGIN                                                              $
  thm_cotrans,in_name[0,j],out_name[0,j],OUT_COORD=coords[1],IN_COORD=coords[0]  & $
  thm_cotrans,in_name[1,j],out_name[1,j],OUT_COORD=coords[1],IN_COORD=coords[0]  & $
  thm_cotrans,in_name[2,j],out_name[2,j],OUT_COORD=coords[1],IN_COORD=coords[0]

names     = tnames([REFORM(in_name,3*2),REFORM(out_name,3*2)])
options,names,'COLORS',[250,150, 50],/DEF
options,names,'LABELS',['x','y','z'],/DEF

dens_nm = tnames('*b_density')
temp_nm = tnames('*b_avgtemp')
options,dens_nm,'YLOG',0,/DEF
options,temp_nm,'YLOG',0,/DEF

nnw = tnames()
options,nnw,'YSTYLE',1
options,nnw,'PANEL_SIZE',2.
options,nnw,'XMINOR',5
options,nnw,'XTICKLEN',0.04
options,nnw,'YTICKLEN',0.01
;-----------------------------------------------------------------------------------------
; => Load ESA DFs
;-----------------------------------------------------------------------------------------
coord   = 'gse'
sc      = probe[0]
pref    = 'th'+sc[0]+'_'
velname = pref[0]+'peib_velocity_'+coord[0]
magname = pref[0]+'fgh_'+coord[0]

; => Load IESA DFs [with THETA and PHI in GSE Coords]
format  = pref[0]+'peib'
peib_df_arr_b = thm_part_dist_array(FORMAT=format[0],TRANGE=tr,MAG_DATA=magname[0],$
                                    VEL_DATA=velname[0])

; => Load IESA DFs [with THETA and PHI in GSE Coords]
velname = pref[0]+'peeb_velocity_'+coord[0]
format  = pref[0]+'peeb'
peeb_df_arr_b = thm_part_dist_array(FORMAT=format[0],TRANGE=tr,MAG_DATA=magname[0],$
                                    VEL_DATA=velname[0])
;-----------------------------------------------------------------------------------------
; => Set up plot windows and defaults
;-----------------------------------------------------------------------------------------
WINDOW,1,RETAIN=2,XSIZE=800,YSIZE=1100
WINDOW,2,RETAIN=2,XSIZE=800,YSIZE=1100
WINDOW,3,RETAIN=2,XSIZE=800,YSIZE=1100

; => setup colors
LOADCT,39
DEVICE,DECOMPOSED=0


; => Define shock parameters [from RH solutions]
vshn_0  =  -16.969794d0
ushn_0  = -208.86162d0
gnorm_0 = [ 0.65137484d0,-0.056795786d0,-0.73929364d0]
; => Vsw [GSE], |Vsw| (km/s)
vsw_up0 = [-252.883d0,-7.32447d0,83.2220d0]
vmag_u0 = NORM(vsw_up0)
; => B [GSE], |B| (nT)
mag_up0 = [-2.88930d0,1.79045d0,-1.37976d0]
bmag_u0 = NORM(mag_up0)

; => flip solutions to outward normal [contour min in opposite direction]
vshn_1  = -1d0*8.9187740d0
ushn_1  = -1d0*271.77714d0
gnorm_1 = -1d0*[-0.98198796d0,-0.11510345d0,0.0070259621d0]
; => Vsw [GSE], |Vsw| (km/s)
vsw_up1 = [-284.261d0,-14.5620d0,-17.2934d0]
vmag_u1 = NORM(vsw_up1)
; => B [GSE], |B| (nT)
mag_up1 = [-2.16373d0,-1.43950d0,1.97681d0]
bmag_u1 = NORM(mag_up1)

vshn_2  = vshn_1[0]
ushn_2  = ushn_1[0]
gnorm_2 = gnorm_1
vsw_up2 = vsw_up1
vmag_u2 = vmag_u1
mag_up2 = mag_up1
bmag_u2 = bmag_u1
;-----------------------------------------------------
; => Calculate gyrospeeds of specular reflection
;-----------------------------------------------------

; => calculate unit vectors
bhat0    = mag_up0/bmag_u0[0]
vhat0    = vsw_up0/vmag_u0[0]
bhat1    = mag_up1/bmag_u1[0]
vhat1    = vsw_up1/vmag_u1[0]
bhat2    = bhat1
vhat2    = vhat1
; => calculate upstream inflow velocity
v_up_0   = vsw_up0 - gnorm_0*vshn_0[0]
v_up_1   = vsw_up1 - gnorm_1*vshn_1[0]
v_up_2   = v_up_1
; => Eq. 2 from Gosling et al., [1982]
;      [specularly reflected ion velocity]
Vref_0   = v_up_0 - gnorm_0*(2d0*my_dot_prod(v_up_0,gnorm_0,/NOM))
Vref_1   = v_up_1 - gnorm_1*(2d0*my_dot_prod(v_up_1,gnorm_1,/NOM))
Vref_2   = Vref_1
; => Eq. 4 and 3 from Gosling et al., [1982]
;      [guiding center velocity of a specularly reflected ion]
Vper_r0  = v_up_0 - bhat0*my_dot_prod(v_up_0,bhat0,/NOM)  ; => Eq. 4
Vper_r1  = v_up_1 - bhat1*my_dot_prod(v_up_1,bhat1,/NOM)  ; => Eq. 4
Vper_r2  = Vper_r1
Vgc_r_0  = Vper_r0 + bhat0*(my_dot_prod(Vref_0,bhat0,/NOM))
Vgc_r_1  = Vper_r1 + bhat1*(my_dot_prod(Vref_1,bhat1,/NOM))
Vgc_r_2  = Vgc_r_1
; => Eq. 6 from Gosling et al., [1982]
;      [gyro-velocity of a specularly reflected ion]
Vgy_r_0  = Vref_0 - Vgc_r_0
Vgy_r_1  = Vref_1 - Vgc_r_1
Vgy_r_2  = Vgy_r_1
; => Eq. 7 and 9 from Gosling et al., [1982]
;      [guiding center velocity of a specularly reflected ion perp. to shock surface]
Vgcn_r_0 = my_dot_prod(Vgc_r_0,gnorm_0,/NOM)
Vgcn_r_1 = my_dot_prod(Vgc_r_1,gnorm_1,/NOM)
Vgcn_r_2 = Vgcn_r_1
;      [guiding center velocity of a specularly reflected ion para. to B-field]
Vgcb_r_0 = my_dot_prod(Vgc_r_0,bhat0,/NOM)
Vgcb_r_1 = my_dot_prod(Vgc_r_1,bhat1,/NOM)
Vgcb_r_2 = Vgcb_r_1
; => gyrospeed
Vgy_rs_0 = NORM(REFORM(Vgy_r_0))
Vgy_rs_1 = NORM(REFORM(Vgy_r_1))
Vgy_rs_2 = Vgy_rs_1

PRINT,';', Vgy_rs_0[0], Vgy_rs_1[0], Vgy_rs_2[0]
;       398.23721       376.68713       376.68713
;-----------------------------------------------------
; IESA
;-----------------------------------------------------
i_time0 = peib_df_arr_b.TIME
i_time1 = peib_df_arr_b.END_TIME
tbow0   = time_double(tdate[0]+'/'+['08:56:00.000','09:10:00.000'])
tbow1   = time_double(tdate[0]+'/'+['09:14:00.000','09:30:00.000'])
tbow2   = time_double(tdate[0]+'/'+['09:30:00.000','09:44:00.000'])
good_i0 = WHERE(i_time0 GE tbow0[0] AND i_time1 LE tbow0[1],gdi0)
good_i1 = WHERE(i_time0 GE tbow1[0] AND i_time1 LE tbow1[1],gdi1)
good_i2 = WHERE(i_time0 GE tbow2[0] AND i_time1 LE tbow2[1],gdi2)
PRINT,';', gdi0, gdi1, gdi2
;         281         318         279

dat_i0  = peib_df_arr_b[good_i0]
dat_i1  = peib_df_arr_b[good_i1]
dat_i2  = peib_df_arr_b[good_i2]
;-----------------------------------------------------------------------------------------
; => Check to make sure these are working
;-----------------------------------------------------------------------------------------
dat   = dat_i0[0]
test  = test_3dp_struc_format(dat)

test  = test_wind_vs_themis_esa_struct(dat)
strnm = dat_themis_esa_str_names(dat)

modify_themis_esa_struc,dat
HELP,dat[0],/STR     ; => show you the changes

; => compare unit conversion routines to make sure they match
data  = conv_units(dat,'df')
data2 = conv_units(dat_i0[0],'df')
PRINT,';  ',minmax(data.DATA,/POS)
PRINT,';  ',minmax(data2.DATA,/POS)
;-----------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------
;  Plot individual contour plots
;-----------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------
WINDOW,1,RETAIN=2,XSIZE=800,YSIZE=1100
WINDOW,2,RETAIN=2,XSIZE=800,YSIZE=1100
WINDOW,3,RETAIN=2,XSIZE=800,YSIZE=1100

dat_aa  = dat_i0
; => Add necessary tags to allow structure format testing routines in
;      contour_3d_1plane.pro etc. to think the input structure is a Wind 3DP structure
modify_themis_esa_struc,dat_aa

ngrid   = 30L               ; => # of grid points to use
sunv    = [1.,0.,0.]
sunn    = 'Sun Dir.'        ; => name of extra vector
xname   = 'B!Do!N'          ; => name of VEC1 (see below)
yname   = 'V!Dsw!N'         ; => name of VEC2 (see below)

vlim    = 25e2              ; => velocity limit for contour and X-axis of cuts
; => Define the # of points to smooth the cuts of the DF
ns      = 7L
smc     = 1
smct    = 1
; => Define the min/max allowable range in DF plots
dfmax   = 1d-1
dfmin   = 1d-15

j       = 79L
tr_ie   = tbow0
dat_i   = dat_aa[j]
gnorm   = gnorm_0
normnm  = 'Shock Normal[0]'
vcirc   = Vgy_rs_0[0]       ; => Put a circle of constant energy at the gyrospeed

WSET,0
mode    = 'fgh'
names   = 'th'+sc[0]+'_'+[mode[0]+['_mag','_gsm'],'efw_cal_gsm','scw_gsm_L2']
  tplot,names,/NOM,TRANGE=tr_ie
  time_bar,dat_i[0].TIME,VARNAME=names,COLOR=250L
  time_bar,dat_i[0].END_TIME,VARNAME=names,COLOR= 50L

; => Define the two vectors which define the XY-plane
vec1    = dat_i.MAGF
vec2    = dat_i.VSW

WSET,1
WSHOW,1
contour_3d_1plane,dat_i,vec1,vec2,VLIM=vlim,NGRID=ngrid,XNAME=xname,    $
                      YNAME=yname,SM_CUTS=smc,NSMOOTH=ns,/ONE_C,        $
                      DFRA=dfra,VCIRC=vcirc[0],PLANE='xy',EX_VEC1=sunv, $
                      EX_VN1=sunn[0],EX_VEC0=gnorm,EX_VN0=normnm[0],    $
                      SM_CONT=smct,/NO_REDF,INTERP=inter,DFMIN=dfmin[0],$
                      DFMAX=dfmax[0]

WSET,2
WSHOW,2
contour_3d_1plane,dat_i,vec1,vec2,VLIM=vlim,NGRID=ngrid,XNAME=xname,    $
                      YNAME=yname,SM_CUTS=smc,NSMOOTH=ns,/ONE_C,        $
                      DFRA=dfra,VCIRC=vcirc[0],PLANE='xz',EX_VEC1=sunv, $
                      EX_VN1=sunn[0],EX_VEC0=gnorm,EX_VN0=normnm[0],    $
                      SM_CONT=smct,/NO_REDF,INTERP=inter,DFMIN=dfmin[0],$
                      DFMAX=dfmax[0]

WSET,3
WSHOW,3
contour_3d_1plane,dat_i,vec1,vec2,VLIM=vlim,NGRID=ngrid,XNAME=xname,    $
                      YNAME=yname,SM_CUTS=smc,NSMOOTH=ns,/ONE_C,        $
                      DFRA=dfra,VCIRC=vcirc[0],PLANE='yz',EX_VEC1=sunv, $
                      EX_VN1=sunn[0],EX_VEC0=gnorm,EX_VN0=normnm[0],    $
                      SM_CONT=smct,/NO_REDF,INTERP=inter,DFMIN=dfmin[0],$
                      DFMAX=dfmax[0]
;-----------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------
;  Save each individual contour plot
;-----------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/contour_df_pos_slide_plot.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/contour_df_pos_slide_plot.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/contour_df_pos_slide_wrapper.pro

timename   = ['thb_fgh_mag','thb_fgh_gse']
dat_aa     = dat_i0
modify_themis_esa_struc,dat_aa
ngrid      = 30L
xname      = 'B!Do!N'
yname      = 'V!Dsw!N'
vlim       = 25e2
ns         = 7L
smc        = 1
smct       = 1
dfmax      = 1d-1
dfmin      = 1d-15
gnorm      = gnorm_0
normnm     = 'Shock Normal[0]'
vcirc      = Vgy_rs_0[0]
; change the following to whatever time range you desire
tbow00     = time_double(tdate[0]+'/'+['09:00:00.000','09:06:40.000'])
; => Plot and save snapshots
contour_df_pos_slide_wrapper,timename,dat_aa,VLIM=vlim[0],NGRID=ngrid[0],XNAME=xname[0], $
                              YNAME=yname[0],VCIRC=vcirc[0],EX_VEC0=gnorm,      $
                              EX_VN0=normnm[0],PLANE=plane,SM_CONT=smct,$
                              DFMIN=dfmin[0],DFMAX=dfmax[0],TRANGE=tbow00, $
                              TROUTER=21d0,SNAPSHOTS=1,TSNAMES='FGM-fgh-GSE_'


;-----------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------
;  Make a short movie
;-----------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/contour_df_pos_slide_plot.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/contour_df_pos_slide_plot.pro
.compile /Users/lbwilson/Desktop/swidl-0.1/wind_3dp_pros/LYNN_PRO/contour_df_pos_slide_wrapper.pro

timename   = ['thb_fgh_mag','thb_fgh_gse']
dat_aa     = dat_i0
modify_themis_esa_struc,dat_aa
ngrid      = 30L
xname      = 'B!Do!N'
yname      = 'V!Dsw!N'
vlim       = 25e2
ns         = 7L
smc        = 1
smct       = 1
dfmax      = 1d-1
dfmin      = 1d-15
gnorm      = gnorm_0
normnm     = 'Shock Normal[0]'
vcirc      = Vgy_rs_0[0]
; change the following to whatever time range you desire
tbow00     = time_double(tdate[0]+'/'+['09:00:00.000','09:02:00.000'])
; make the movie with 4 copies of each frame [EXFRAMES keyword] so that when played back
;   it moves slow enough to watch
contour_df_pos_slide_wrapper,timename,dat_aa,VLIM=vlim[0],NGRID=ngrid[0],XNAME=xname[0], $
                              YNAME=yname[0],VCIRC=vcirc[0],EX_VEC0=gnorm,               $
                              EX_VN0=normnm[0],PLANE=plane,SM_CONT=smct,                 $
                              DFMIN=dfmin[0],DFMAX=dfmax[0],TRANGE=tbow00,               $
                              TROUTER=21d0,TSNAMES='FGM-fgh-GSE_',EXFRAMES=4L

















